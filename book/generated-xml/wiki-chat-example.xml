<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE article PUBLIC "-//OASIS//DTD DocBook XML V4.5//EN" "http://www.oasis-open.org/docbook/xml/4.5/docbookx.dtd">
<?asciidoc-toc?>
<?asciidoc-numbered?>

<article lang="en">
<section id="wiki-chat-example_wiki_markdown_chat_subsite_event_source">
<title>Wiki: markdown, chat subsite, event source</title>
<simpara>This example will tie together a few different ideas. We&#8217;ll start with a chat
subsite, which allows us to embed a chat widget on any page. We&#8217;ll use the HTML
5 event source API to handle sending events from the server to the client.</simpara>
<section id="wiki-chat-example_subsite_data">
<title>Subsite: data</title>
<simpara>In order to define a subsite, we first need to create a foundation type for the
subsite, the same as we would do for a normal Yesod application. In our case,
we want to keep a channel of all the events to be sent to the individual
participants of a chat. This ends up looking like:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat/Data.hs
{-# LANGUAGE FlexibleContexts      #-}
{-# LANGUAGE FlexibleInstances     #-}
{-# LANGUAGE OverloadedStrings     #-}
{-# LANGUAGE QuasiQuotes           #-}
{-# LANGUAGE RankNTypes            #-}
{-# LANGUAGE TemplateHaskell       #-}
{-# LANGUAGE TypeFamilies          #-}
module Chat.Data where

import           Blaze.ByteString.Builder.Char.Utf8  (fromText)
import           Control.Concurrent.Chan
import           Data.Monoid                         ((&lt;&gt;))
import           Data.Text                           (Text)
import           Network.Wai.EventSource
import           Network.Wai.EventSource.EventStream
import           Yesod
import           Yesod.Core.Types (SubHandlerFor)

-- | Our subsite foundation. We keep a channel of events that all connections
-- will share.
data Chat = Chat (Chan ServerEvent)</programlisting>
<simpara>We also need to define our subsite routes in the same module. We need to have
two commands: one to send a new message to all users, and another to receive
the stream of messages.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat/Data.hs
mkYesodSubData "Chat" [parseRoutes|
/send SendR POST
/recv ReceiveR GET
|]</programlisting>
</section>
<section id="wiki-chat-example_subsite_handlers">
<title>Subsite: handlers</title>
<simpara>Now that we&#8217;ve defined our foundation and routes, we need to create a separate
module for providing the subsite dispatch functionality. We&#8217;ll call this
module <literal>Chat</literal>, and it&#8217;s where we&#8217;ll start to see how a subsite functions.</simpara>
<simpara>A subsite always sits as a layer on top of some master site, which will be
provided by the user. In many cases, a subsite will require specific
functionality to be present in the master site. In the case of our chat
subsite, we want user authentication to be provided by the master site. The
subsite needs to be able to query whether the current user is logged into the
site, and to get the user&#8217;s name.</simpara>
<simpara>The way we represent this concept is to define a typeclass that encapsulates
the necessary functionality. Let&#8217;s have a look at our <literal>YesodChat</literal> typeclass:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat/Data.hs
class (Yesod master, RenderMessage master FormMessage)
        =&gt; YesodChat master where
    getUserName :: HandlerFor master Text
    isLoggedIn :: HandlerFor master Bool</programlisting>
<simpara>Any master site which wants to use the chat subsite will need to provide a
<literal>YesodChat</literal> instance. (We&#8217;ll see in a bit how this requirement is enforced.)
There are a few interesting things to note:</simpara>
<itemizedlist>
<listitem>
<simpara>
We can put further constraints on the master site, such as providing a
  <literal>Yesod</literal> instance and allowing rendering of form messages. The former allows
  us to use <literal>defaultLayout</literal>, while the latter allows us to use standard form
  widgets.
</simpara>
</listitem>
<listitem>
<simpara>
Previously in the book, we&#8217;ve used the <literal>Handler</literal> monad quite a bit. Remember
  that <literal>Handler</literal> is just an application-specific type synonym around
  <literal>HandlerFor</literal>. Since this code is intended to work with many different
  applications, we use the full <literal>HandlerFor</literal> form of the transformer.
</simpara>
</listitem>
</itemizedlist>
<simpara>Speaking of the <literal>Handler</literal> type synonym, we&#8217;re going to want to have
something similar for our subsite. The question is: what does this
monad look like? In a subsite situation, we use <literal>SubHandlerFor</literal> with
both the subsite data type and the master site type. We&#8217;ll define a
helper synonym for this which requires a <literal>YesodChat</literal> instance on the
master site type, so we end up with:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat/Data.hs
type ChatHandler a =
    forall master. YesodChat master =&gt;
    SubHandlerFor Chat master a</programlisting>
<simpara>Now that we have our machinery out of the way, it&#8217;s time to write our subsite
handler functions. We had two routes: one for sending messages, and one for
receiving messages. Let&#8217;s start with sending. We need to:</simpara>
<orderedlist numeration="arabic">
<listitem>
<simpara>
Get the username for the person sending the message.
</simpara>
</listitem>
<listitem>
<simpara>
Parse the message from the incoming parameters. (Note that we&#8217;re going to use GET parameters for simplicity of the client-side Ajax code.)
</simpara>
</listitem>
<listitem>
<simpara>
Write the message to the <literal>Chan</literal>.
</simpara>
</listitem>
</orderedlist>
<simpara>The trickiest bit of all this code is to know when to use <literal>lift</literal>. Let&#8217;s look at
the implementation, and then discuss those <literal>lift</literal> usages:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat/Data.hs
postSendR :: ChatHandler ()
postSendR = do
    from &lt;- liftHandler getUserName
    body &lt;- runInputGet $ ireq textField "message"
    Chat chan &lt;- getSubYesod
    liftIO $ writeChan chan $ ServerEvent Nothing Nothing $ return $
        fromText from &lt;&gt; fromText ": " &lt;&gt; fromText body</programlisting>
<simpara><literal>getUserName</literal> is the function we defined in our <literal>YesodChat</literal> typeclass earlier.
If we look at that type signature, we see that it lives in the master site&#8217;s
<literal>Handler</literal> monad. Therefore, we need to <literal>lift</literal> that call out of the subsite.</simpara>
<simpara>The call to <literal>runInputGet</literal> is a little more subtle. Theoretically, we could run
this in either the subsite or the master site. However, we use <literal>lift</literal> here as
well for one specific reason: message translations. By using the master site,
we can take advantage of whatever <literal>RenderMessage</literal> instance the master site
defines. This also explains why we have a <literal>RenderMessage</literal> constraint on the
<literal>YesodChat</literal> typeclass.</simpara>
<simpara>The next call to <literal>getSubYesod</literal> is <emphasis>not</emphasis> <literal>lift</literal>ed. The reasoning here is simple:
we want to get the subsite&#8217;s foundation type in order to access the message
channel. If we instead <literal>lift</literal>ed that call, we&#8217;d get the master site&#8217;s
foundation type instead, which is not what we want in this case.</simpara>
<simpara>The final line puts the new message into the channel. Since this is an <literal>IO</literal>
action, we use <literal>liftIO</literal>. <literal>ServerEvent</literal> is part of the <literal>wai-eventsource</literal>
package, and is the means by which we&#8217;re providing server-sent events in this
example.</simpara>
<simpara>The receiving side is similarly simple:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat/Data.hs
getReceiveR :: ChatHandler ()
getReceiveR = do
    Chat chan &lt;- getSubYesod
    sendWaiApplication $ eventSourceAppChan chan</programlisting>
<simpara>The last line in our function exposes the underlying <literal>wai-eventsource</literal>
application as a Yesod handler, using the <literal>sendWaiApplication</literal> function to
promote a WAI application to a Yesod handler. <literal>eventSourceAppChan</literal> duplicates
the chan under the hood, which is a standard method in concurrent Haskel
of creating broadcast channels.</simpara>
<simpara>Now that we&#8217;ve defined our handler functions, we can set up our dispatch. In a
normal application, dispatching is handled by calling <literal>mkYesod</literal>, which creates
the appropriate <literal>YesodDispatch</literal> instance. In subsites, things are a little bit
more complicated, since you&#8217;ll often want to place constraints on the master
site. The formula we use is the following:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat.hs
{-# LANGUAGE FlexibleContexts      #-}
{-# LANGUAGE FlexibleInstances     #-}
{-# LANGUAGE MultiParamTypeClasses #-}
{-# LANGUAGE OverloadedStrings     #-}
{-# LANGUAGE QuasiQuotes           #-}
{-# LANGUAGE RankNTypes            #-}
{-# LANGUAGE TemplateHaskell       #-}
{-# LANGUAGE TypeFamilies          #-}
module Chat where

import           Chat.Data
import           Yesod

instance YesodChat master =&gt; YesodSubDispatch Chat master where
    yesodSubDispatch = $(mkYesodSubDispatch resourcesChat)</programlisting>
<simpara>We&#8217;re stating that our <literal>Chat</literal> subsite can live on top of any master site which
is an instance of <literal>YesodChat</literal>. We then use the <literal>mkYesodSubDispatch</literal> Template
Haskell function to generate all of our dispatching logic. While this is a bit
more difficult to write than <literal>mkYesod</literal>, it provides necessary flexibility, and
is mostly identical for any subsite you&#8217;ll write.</simpara>
</section>
<section id="wiki-chat-example_subsite_widget">
<title>Subsite: widget</title>
<simpara>We now have a fully working subsite. The final component we want as part of our
chat library is a widget to be embedded inside a page which will provide chat
functionality. By creating this as a widget, we can include all of our HTML,
CSS, and Javascript as a reusable component.</simpara>
<simpara>Our widget will need to take in one argument: a function to convert a <literal>Chat</literal>
subsite URL into a master site URL. The reasoning here is that an application
developer could place the chat subsite anywhere in the URL structure, and this
widget needs to be able to generate Javascript which will point at the correct
URLs. Let&#8217;s start off our widget:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat.hs
chatWidget :: YesodChat master
           =&gt; (Route Chat -&gt; Route master)
           -&gt; WidgetFor master ()
chatWidget toMaster = do</programlisting>
<simpara>Next, we&#8217;re going to generate some identifiers to be used by our widget. It&#8217;s
always good practice to let Yesod generate unique identifiers for you instead
of creating them manually to avoid name collisions.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat.hs
    chat &lt;- newIdent   -- the containing div
    output &lt;- newIdent -- the box containing the messages
    input &lt;- newIdent  -- input field from the user</programlisting>
<simpara>And next we need to check if the user is logged in, using the <literal>isLoggedIn</literal>
function in our <literal>YesodChat</literal> typeclass. Since we&#8217;re in a <literal>Widget</literal> and that
function lives in the <literal>Handler</literal> monad, we need to use <literal>handlerToWidget</literal>:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat.hs
    ili &lt;- handlerToWidget isLoggedIn  -- check if we're already logged in</programlisting>
<simpara>If the user is logged in, we want to display the chat box, style it with some
CSS, and then make it interactive using some Javascript. This is mostly
client-side code wrapped in a Widget:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat.hs
    if ili
        then do
            -- Logged in: show the widget
            [whamlet|
                &lt;div ##{chat}&gt;
                    &lt;h2&gt;Chat
                    &lt;div ##{output}&gt;
                    &lt;input ##{input} type=text placeholder="Enter Message"&gt;
            |]
            -- Just some CSS
            toWidget [lucius|
                ##{chat} {
                    position: absolute;
                    top: 2em;
                    right: 2em;
                }
                ##{output} {
                    width: 200px;
                    height: 300px;
                    border: 1px solid #999;
                    overflow: auto;
                }
            |]
            -- And now that Javascript
            toWidgetBody [julius|
                // Set up the receiving end
                var output = document.getElementById(#{toJSON output});
                var src = new EventSource("@{toMaster ReceiveR}");
                src.onmessage = function(msg) {
                    // This function will be called for each new message.
                    var p = document.createElement("p");
                    p.appendChild(document.createTextNode(msg.data));
                    output.appendChild(p);

                    // And now scroll down within the output div so the most recent message
                    // is displayed.
                    output.scrollTop = output.scrollHeight;
                };

                // Set up the sending end: send a message via Ajax whenever the user hits
                // enter.
                var input = document.getElementById(#{toJSON input});
                input.onkeyup = function(event) {
                    var keycode = (event.keyCode ? event.keyCode : event.which);
                    if (keycode == '13') {
                        var xhr = new XMLHttpRequest();
                        var val = input.value;
                        input.value = "";
                        var params = "?message=" + encodeURI(val);
                        xhr.open("POST", "@{toMaster SendR}" + params);
                        xhr.send(null);
                    }
                }
            |]</programlisting>
<simpara>And finally, if the user isn&#8217;t logged in, we&#8217;ll ask them to log in to use the
chat app.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @Chat.hs
        else do
            -- User isn't logged in, give a not-logged-in message.
            master &lt;- getYesod
            [whamlet|
                &lt;p&gt;
                    You must be #
                    $maybe ar &lt;- authRoute master
                        &lt;a href=@{ar}&gt;logged in
                    $nothing
                        logged in
                    \ to chat.
            |]</programlisting>
</section>
<section id="wiki-chat-example_master_site_data">
<title>Master site: data</title>
<simpara>Now we can proceed with writing our main application. This application will
include the chat subsite and a wiki. The first thing we need to consider is how
to store the wiki contents. Normally, we&#8217;d want to put this in some kind of a
Persistent database. For simplicity, we&#8217;ll just use an in-memory
representation. Each Wiki page is indicated by a list of names, and the contents of each page is going to be a piece of <literal>Text</literal>. So our full foundation datatype is:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @ChatMain.hs
{-# LANGUAGE MultiParamTypeClasses #-}
{-# LANGUAGE OverloadedStrings     #-}
{-# LANGUAGE QuasiQuotes           #-}
{-# LANGUAGE TemplateHaskell       #-}
{-# LANGUAGE TypeFamilies          #-}
{-# LANGUAGE ViewPatterns          #-}
module ChatMain where

import           Chat
import           Chat.Data
import           Control.Concurrent.Chan (newChan)
import           Data.IORef
import           Data.Map                (Map)
import qualified Data.Map                as Map
import           Data.Text               (Text)
import qualified Data.Text.Lazy          as TL
import           Text.Markdown
import           Yesod
import           Yesod.Auth
import           Yesod.Auth.Dummy
import           System.SetEnv

data App = App
    { getChat     :: Chat
    , wikiContent :: IORef (Map [Text] Text)
    }</programlisting>
<simpara>Next we want to set up our routes:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @ChatMain.hs
mkYesod "App" [parseRoutes|
/            HomeR GET      -- the homepage
/wiki/*Texts WikiR GET POST -- note the multipiece for the wiki hierarchy

/chat        ChatR Chat getChat    -- the chat subsite
/auth        AuthR Auth getAuth    -- the auth subsite
|]</programlisting>
</section>
<section id="wiki-chat-example_master_site_instances">
<title>Master site: instances</title>
<simpara>We need to make two modifications to the default <literal>Yesod</literal> instance. Firstly, we
want to provide an implementation of <literal>authRoute</literal>, so that our chat subsite
widget can provide a proper link to a login page. Secondly, we&#8217;ll provide a
override to the <literal>defaultLayout</literal>. Besides providing login/logout links, this
function will add in the chat widget on every page.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @ChatMain.hs
instance Yesod App where
    authRoute _ = Just $ AuthR LoginR -- get a working login link

    -- Our custom defaultLayout will add the chat widget to every page.
    -- We'll also add login and logout links to the top.
    defaultLayout widget = do
        pc &lt;- widgetToPageContent $ do
            widget
            chatWidget ChatR
        mmsg &lt;- getMessage
        withUrlRenderer
            [hamlet|
                $doctype 5
                &lt;html&gt;
                    &lt;head&gt;
                        &lt;title&gt;#{pageTitle pc}
                        ^{pageHead pc}
                    &lt;body&gt;
                        $maybe msg &lt;- mmsg
                            &lt;div .message&gt;#{msg}
                        &lt;nav&gt;
                            &lt;a href=@{AuthR LoginR}&gt;Login
                            \ | #
                            &lt;a href=@{AuthR LogoutR}&gt;Logout
                        ^{pageBody pc}
            |]</programlisting>
<simpara>Since we&#8217;re using the chat subsite, we have to provide an instance of
<literal>YesodChat</literal>.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @ChatMain.hs
instance YesodChat App where
    getUserName = do
        muid &lt;- maybeAuthId
        case muid of
            Nothing -&gt; do
                setMessage "Not logged in"
                redirect $ AuthR LoginR
            Just uid -&gt; return uid
    isLoggedIn = do
        ma &lt;- maybeAuthId
        return $ maybe False (const True) ma</programlisting>
<simpara>Our <literal>YesodAuth</literal> and <literal>RenderMessage</literal> instances, as well as the homepage handler,
are rather bland:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @ChatMain.hs
-- Fairly standard YesodAuth instance. We'll use the dummy plugin so that you
-- can create any name you want, and store the login name as the AuthId.
instance YesodAuth App where
    type AuthId App = Text
    authPlugins _ = [authDummy]
    loginDest _ = HomeR
    logoutDest _ = HomeR
    getAuthId = return . Just . credsIdent
    maybeAuthId = lookupSession "_ID"

instance RenderMessage App FormMessage where
    renderMessage _ _ = defaultFormMessage

-- Nothing special here, just giving a link to the root of the wiki.
getHomeR :: Handler Html
getHomeR = defaultLayout
    [whamlet|
        &lt;p&gt;Welcome to the Wiki!
        &lt;p&gt;
            &lt;a href=@{wikiRoot}&gt;Wiki root
    |]
  where
    wikiRoot = WikiR []</programlisting>
</section>
<section id="wiki-chat-example_master_site_wiki_handlers">
<title>Master site: wiki handlers</title>
<simpara>Now it&#8217;s time to write our wiki handlers: a GET for displaying a page, and a
POST for updating a page. We&#8217;ll also define a <literal>wikiForm</literal> function to be used on
both handlers:</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @ChatMain.hs
-- A form for getting wiki content
wikiForm :: Maybe Textarea -&gt; Html -&gt; MForm Handler (FormResult Textarea, Widget)
wikiForm mtext = renderDivs $ areq textareaField "Page body" mtext

-- Show a wiki page and an edit form
getWikiR :: [Text] -&gt; Handler Html
getWikiR page = do
    -- Get the reference to the contents map
    icontent &lt;- fmap wikiContent getYesod

    -- And read the map from inside the reference
    content &lt;- liftIO $ readIORef icontent

    -- Lookup the contents of the current page, if available
    let mtext = Map.lookup page content

    -- Generate a form with the current contents as the default value.
    -- Note that we use the Textarea wrapper to get a &lt;textarea&gt;.
    (form, _) &lt;- generateFormPost $ wikiForm $ fmap Textarea mtext
    defaultLayout $ do
        case mtext of
            -- We're treating the input as markdown. The markdown package
            -- automatically handles XSS protection for us.
            Just text -&gt; toWidget $ markdown def $ TL.fromStrict text
            Nothing -&gt; [whamlet|&lt;p&gt;Page does not yet exist|]
        [whamlet|
            &lt;h2&gt;Edit page
            &lt;form method=post&gt;
                ^{form}
                &lt;div&gt;
                    &lt;input type=submit&gt;
        |]

-- Get a submitted wiki page and updated the contents.
postWikiR :: [Text] -&gt; Handler Html
postWikiR page = do
    icontent &lt;- fmap wikiContent getYesod
    content &lt;- liftIO $ readIORef icontent
    let mtext = Map.lookup page content
    ((res, form), _) &lt;- runFormPost $ wikiForm $ fmap Textarea mtext
    case res of
        FormSuccess (Textarea t) -&gt; do
            liftIO $ atomicModifyIORef icontent $
                \m -&gt; (Map.insert page t m, ())
            setMessage "Page updated"
            redirect $ WikiR page
        _ -&gt; defaultLayout
                [whamlet|
                    &lt;form method=post&gt;
                        ^{form}
                        &lt;div&gt;
                            &lt;input type=submit&gt;
                |]</programlisting>
</section>
<section id="wiki-chat-example_master_site_running">
<title>Master site: running</title>
<simpara>Finally, we&#8217;re ready to run our application. Unlike many of our previous
examples in this book, we need to perform some real initialization in the
<literal>main</literal> function. The <literal>Chat</literal> subsite requires an empty <literal>Chan</literal> to be created, and
we need to create a mutable variable to hold the wiki contents. Once we have
those values, we can create an <literal>App</literal> value and pass it to the <literal>warp</literal> function.</simpara>
<programlisting language="haskell" linenumbering="unnumbered">-- @ChatMain.hs
main :: IO ()
main = do
    -- Create our server event channel
    chan &lt;- newChan

    -- Initially have a blank database of wiki pages
    icontent &lt;- newIORef Map.empty

    -- Set web server's listening port required by warpEnv function
    -- This env var is set up automatically if 'yesod devel' is used
    setEnv "PORT" "3000"

    -- Run our app
    warpEnv App
        { getChat = Chat chan
        , wikiContent = icontent
        }</programlisting>
</section>
<section id="wiki-chat-example_conclusion">
<title>Conclusion</title>
<simpara>This example demonstrated creation of a non-trivial subsite. Some important
points to notice were the usage of typeclasses to express constraints on the
master site, how data initialization was performed in the <literal>main</literal> function, and
how <literal>lift</literal>ing allowed us to operate in either the subsite or master site
context.</simpara>
<simpara>If you&#8217;re looking for a way to test out your subsite skills, I&#8217;d recommend
modifying this example so that the Wiki code also lived in its own subsite.</simpara>
</section>
</section>
</article>
