<?xml version="1.0" encoding="UTF-8"?>
<html>
  <pre id="x-3">This is a simple blog app. It allows an admin to add blog posts via a rich text
editor (nicedit), allows logged-in users to comment, and has full i18n support.
It is also a good example of using a Persistent database, leveraging Yesod's
authorization system, and templates.

While in general we recommend placing templates, Persist entity definitions,
and routing in separate files, we'll keep it all in one file here for
convenience. The one exception you'll see below will be i18n messages.

We'll start off with our language extensions. In scaffolded code, the language
extensions are specified in the cabal file, so you won't need to put this in
your individual Haskell files.

&gt; {-# LANGUAGE OverloadedStrings, TypeFamilies, QuasiQuotes,
&gt;              TemplateHaskell, GADTs, FlexibleContexts,
&gt;              MultiParamTypeClasses #-}

Now our imports.

&gt; import Yesod
&gt; import Yesod.Auth
&gt; import Yesod.Form.Nic (YesodNic, nicHtmlField)
&gt; import Yesod.Auth.BrowserId (authBrowserId)
&gt; import Data.Text (Text)
&gt; import Network.HTTP.Conduit (Manager, newManager, def)
&gt; import Database.Persist.Sqlite
&gt;     ( ConnectionPool, SqlPersist, runSqlPool, runMigration
&gt;     , createSqlitePool
&gt;     )
&gt; import Data.Time (UTCTime, getCurrentTime)
&gt; import Control.Applicative ((&lt;$&gt;), (&lt;*&gt;), pure)

First we'll set up our Persistent entities. We're going to both create our data
types (via mkPersist) and create a migration function, which will automatically
create and update our SQL schema. If you were using the MongoDB backend,
migration would not be needed.

&gt; share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persistLowerCase|

Keeps track of users. In a more robust application, we would also keep account
creation date, display name, etc.

&gt; User
&gt;    email Text
&gt;    UniqueUser email

An individual blog entry (I've avoided using the word "post" due to the
confusion with the request method POST).

&gt; Entry
&gt;    title Text
&gt;    posted UTCTime
&gt;    content Html

We need to tack on this "deriving" line since Html doesn't specify instances
for Read, Show or Eq. If you get an error message about "cannot derive" in your
    own code, try adding the deriving statement.

&gt;    deriving

And a comment on the blog post.

&gt; Comment
&gt;    entry EntryId
&gt;    posted UTCTime
&gt;    user UserId
&gt;    name Text
&gt;    text Textarea
&gt; |]

Every site has a foundation datatype. This value is initialized before
launching your application, and is available throughout. We'll store a database
connection pool and HTTP connection manager in ours. See the very end of this
file for how those are initialized.

&gt; data Blog = Blog
&gt;    { connPool :: ConnectionPool
&gt;    , httpManager :: Manager
&gt;    }

To make i18n easy and translator friendly, we have a special file format for
translated messages. There is a single file for each language, and each file is
named based on the language code (e.g., en, es, de-DE) and placed in that
folder. We also specify the main language file (here, "en") as a default
language.

&gt; mkMessage "Blog" "../messages-blog" "en"

Our en message file contains the following content:

    NotAnAdmin: You must be an administrator to access this page.

    WelcomeHomepage: Welcome to the homepage
    SeeArchive: See the archive

    NoEntries: There are no entries in the blog
    LoginToPost: Admins can login to post
    NewEntry: Post to blog
    NewEntryTitle: Title
    NewEntryContent: Content

    PleaseCorrectEntry: Your submitted entry had some errors, please correct and try again.
    EntryCreated title@Text: Your new blog post, #{title}, has been created

    EntryTitle title@Text: Blog post: #{title}
    CommentsHeading: Comments
    NoComments: There are no comments
    AddCommentHeading: Add a Comment
    LoginToComment: You must be logged in to comment
    AddCommentButton: Add comment

    CommentName: Your display name
    CommentText: Comment
    CommentAdded: Your comment has been added
    PleaseCorrectComment: Your submitted comment had some errors, please correct and try again.

    HomepageTitle: Yesod Blog Demo
    BlogArchiveTitle: Blog Archive

Now we're going to set up our routing table. We have four entries: a homepage,
an entry list page (BlogR), an individual entry page (EntryR) and our
authentication subsite. Note that BlogR and EntryR both accept GET and POST
methods. The POST methods are for adding a new blog post and adding a new
comment, respectively.

&gt; mkYesod "Blog" [parseRoutes|
&gt; / RootR GET
&gt; /blog BlogR GET POST
&gt; /blog/#EntryId EntryR GET POST
&gt; /auth AuthR Auth getAuth
&gt; |]

Every foundation needs to be an instance of the Yesod typeclass. This is where
we configure various settings.

&gt; instance Yesod Blog where

The base of our application. Note that in order to make BrowserID work
properly, this must be a valid URL.

&gt;     approot = ApprootStatic "http://localhost:3000"

Our authorization scheme. We want to have the following rules:

* Only admins can add a new entry.
* Only logged in users can add a new comment.
* All other pages can be accessed by anyone.

We set up our routes in a RESTful way, where the actions that could make
changes are always using a POST method. As a result, we can simply check for
whether or not a request is a write request, given by the True in the second
field.

First, we'll authorize requests to add a new entry.

&gt;     isAuthorized BlogR True = do
&gt;         mauth &lt;- maybeAuth
&gt;         case mauth of
&gt;             Nothing -&gt; return AuthenticationRequired
&gt;             Just (Entity _ user)
&gt;                 | isAdmin user -&gt; return Authorized
&gt;                 | otherwise    -&gt; unauthorizedI MsgNotAnAdmin

Now we'll authorize requests to add a new comment.

&gt;     isAuthorized (EntryR _) True = do
&gt;         mauth &lt;- maybeAuth
&gt;         case mauth of
&gt;             Nothing -&gt; return AuthenticationRequired
&gt;             Just _  -&gt; return Authorized

And for all other requests, the result is always authorized.

&gt;     isAuthorized _ _ = return Authorized

Where a user should be redirected to if they get an AuthenticationRequired.

&gt;     authRoute _ = Just (AuthR LoginR)

This is where we define our site look-and-feel. The function is given the
content for the individual page, and wraps it up with a standard template.

&gt;     defaultLayout inside = do

Yesod encourages the get-following-post pattern, where after a POST, the user
is redirected to another page. In order to allow the POST page to give the user
some kind of feedback, we have the getMessage and setMessage functions. It's a
good idea to always check for pending messages in your defaultLayout function.

&gt;         mmsg &lt;- getMessage

We use widgets to compose together HTML, CSS and Javascript. At the end of the
day, we need to unwrap all of that into simple HTML. That's what the
widgetToPageContent function is for. We're going to give it a widget consisting
of the content we received from the individual page (inside), plus a standard
CSS for all pages. We'll use the Lucius template language to create the latter.

&gt;         pc &lt;- widgetToPageContent $ do
&gt;             toWidget [lucius|
&gt; body {
&gt;     width: 760px;
&gt;     margin: 1em auto;
&gt;     font-family: sans-serif;
&gt; }
&gt; textarea {
&gt;     width: 400px;
&gt;     height: 200px;
&gt; }
&gt; #message {
&gt;   color: #900;
&gt; }
&gt; |]
&gt;             inside

And finally we'll use a new Hamlet template to wrap up the individual
components (title, head data and body data) into the final output.

&gt;         hamletToRepHtml [hamlet|
&gt; $doctype 5
&gt; &lt;html&gt;
&gt;     &lt;head&gt;
&gt;         &lt;title&gt;#{pageTitle pc}
&gt;         ^{pageHead pc}
&gt;     &lt;body&gt;
&gt;         $maybe msg &lt;- mmsg
&gt;             &lt;div #message&gt;#{msg}
&gt;         ^{pageBody pc}
&gt; |]

This is a simple function to check if a user is the admin. In a real
application, we would likely store the admin bit in the database itself, or
check with some external system. For now, I've just hard-coded my own email
address.

&gt; isAdmin :: User -&gt; Bool
&gt; isAdmin user = userEmail user == "michael@snoyman.com"

In order to access the database, we need to create a YesodPersist instance,
which says which backend we're using and how to run an action.

&gt; instance YesodPersist Blog where
&gt;    type YesodPersistBackend Blog = SqlPersist
&gt;    runDB f = do 
&gt;        master &lt;- getYesod
&gt;        let pool = connPool master
&gt;        runSqlPool f pool

This is a convenience synonym. It is defined automatically for you in the
scaffolding.

&gt; type Form x = Html -&gt; MForm Blog Blog (FormResult x, Widget)

In order to use yesod-form and yesod-auth, we need an instance of RenderMessage
for FormMessage. This allows us to control the i18n of individual form
messages.

&gt; instance RenderMessage Blog FormMessage where
&gt;     renderMessage _ _ = defaultFormMessage

In order to use the built-in nic HTML editor, we need this instance. We just
take the default values, which use a CDN-hosted version of Nic.

&gt; instance YesodNic Blog

In order to use yesod-auth, we need a YesodAuth instance.

&gt; instance YesodAuth Blog where
&gt;     type AuthId Blog = UserId
&gt;     loginDest _ = RootR
&gt;     logoutDest _ = RootR
&gt;     authHttpManager = httpManager

We'll use [BrowserID](https://browserid.org/), which is a third-party system
using email addresses as your identifier. This makes it easy to switch to other
systems in the future, locally authenticated email addresses (also included
with yesod-auth).

&gt;     authPlugins _ = [authBrowserId]

This function takes someone's login credentials (i.e., his/her email address)
and gives back a UserId.

&gt;     getAuthId creds = do
&gt;         let email = credsIdent creds
&gt;             user = User email
&gt;         res &lt;- runDB $ insertBy user
&gt;         return $ Just $ either entityKey id res

Homepage handler. The one important detail here is our usage of `setTitleI`,
which allows us to use i18n messages for the title. We also use this message
with a `_{Msg...}` interpolation in Hamlet.

&gt; getRootR :: Handler RepHtml
&gt; getRootR = defaultLayout $ do
&gt;     setTitleI MsgHomepageTitle
&gt;     [whamlet|
&gt; &lt;p&gt;_{MsgWelcomeHomepage}
&gt; &lt;p&gt;
&gt;    &lt;a href=@{BlogR}&gt;_{MsgSeeArchive}
&gt; |]

Define a form for adding new entries. We want the user to provide the title and
content, and then fill in the post date automatically via `getCurrentTime`.

&gt; entryForm :: Form Entry
&gt; entryForm = renderDivs $ Entry
&gt;     &lt;$&gt; areq textField (fieldSettingsLabel MsgNewEntryTitle) Nothing
&gt;     &lt;*&gt; aformM (liftIO getCurrentTime)
&gt;     &lt;*&gt; areq nicHtmlField (fieldSettingsLabel MsgNewEntryContent) Nothing

Get the list of all blog entries, and present an admin with a form to create a
new entry.

&gt; getBlogR :: Handler RepHtml
&gt; getBlogR = do
&gt;     muser &lt;- maybeAuth
&gt;     entries &lt;- runDB $ selectList [] [Desc EntryPosted]
&gt;     ((_, entryWidget), enctype) &lt;- generateFormPost entryForm
&gt;     defaultLayout $ do
&gt;         setTitleI MsgBlogArchiveTitle
&gt;         [whamlet|
&gt; $if null entries
&gt;     &lt;p&gt;_{MsgNoEntries}
&gt; $else
&gt;     &lt;ul&gt;
&gt;         $forall Entity entryId entry &lt;- entries
&gt;             &lt;li&gt;
&gt;                 &lt;a href=@{EntryR entryId}&gt;#{entryTitle entry}

We have three possibilities: the user is logged in as an admin, the user is
logged in and is not an admin, and the user is not logged in. In the first
case, we should display the entry form. In the second, we'll do nothing. In the
third, we'll provide a login link.

&gt; $maybe Entity _ user &lt;- muser
&gt;     $if isAdmin user
&gt;         &lt;form method=post enctype=#{enctype}&gt;
&gt;             ^{entryWidget}
&gt;             &lt;div&gt;
&gt;                 &lt;input type=submit value=_{MsgNewEntry}&gt;
&gt; $nothing
&gt;     &lt;p&gt;
&gt;         &lt;a href=@{AuthR LoginR}&gt;_{MsgLoginToPost}
&gt; |]

Process an incoming entry addition. We don't do any permissions checking, since
isAuthorized handles it for us. If the form submission was valid, we add the
entry to the database and redirect to the new entry. Otherwise, we ask the user
to try again.

&gt; postBlogR :: Handler RepHtml
&gt; postBlogR = do
&gt;     ((res, entryWidget), enctype) &lt;- runFormPost entryForm
&gt;     case res of
&gt;         FormSuccess entry -&gt; do
&gt;             entryId &lt;- runDB $ insert entry
&gt;             setMessageI $ MsgEntryCreated $ entryTitle entry
&gt;             redirect $ EntryR entryId
&gt;         _ -&gt; defaultLayout $ do
&gt;             setTitleI MsgPleaseCorrectEntry
&gt;             [whamlet|
&gt; &lt;form method=post enctype=#{enctype}&gt;
&gt;     ^{entryWidget}
&gt;     &lt;div&gt;
&gt;         &lt;input type=submit value=_{MsgNewEntry}&gt;
&gt; |]

A form for comments, very similar to our entryForm above.

&gt; commentForm :: EntryId -&gt; Form Comment
&gt; commentForm entryId = renderDivs $ Comment
&gt;     &lt;$&gt; pure entryId
&gt;     &lt;*&gt; aformM (liftIO getCurrentTime)
&gt;     &lt;*&gt; aformM requireAuthId
&gt;     &lt;*&gt; areq textField (fieldSettingsLabel MsgCommentName) Nothing
&gt;     &lt;*&gt; areq textareaField (fieldSettingsLabel MsgCommentText) Nothing

Show an individual entry, comments, and an add comment form if the user is
logged in.

&gt; getEntryR :: EntryId -&gt; Handler RepHtml
&gt; getEntryR entryId = do
&gt;     (entry, comments) &lt;- runDB $ do
&gt;         entry &lt;- get404 entryId
&gt;         comments &lt;- selectList [] [Asc CommentPosted]
&gt;         return (entry, map entityVal comments)
&gt;     muser &lt;- maybeAuth
&gt;     ((_, commentWidget), enctype) &lt;-
&gt;         generateFormPost (commentForm entryId)
&gt;     defaultLayout $ do
&gt;         setTitleI $ MsgEntryTitle $ entryTitle entry
&gt;         [whamlet|
&gt; &lt;h1&gt;#{entryTitle entry}
&gt; &lt;article&gt;#{entryContent entry}
&gt;     &lt;section .comments&gt;
&gt;         &lt;h1&gt;_{MsgCommentsHeading}
&gt;         $if null comments
&gt;             &lt;p&gt;_{MsgNoComments}
&gt;         $else
&gt;             $forall Comment _entry posted _user name text &lt;- comments
&gt;                 &lt;div .comment&gt;
&gt;                     &lt;span .by&gt;#{name}
&gt;                     &lt;span .at&gt;#{show posted}
&gt;                     &lt;div .content&gt;#{text}
&gt;         &lt;section&gt;
&gt;             &lt;h1&gt;_{MsgAddCommentHeading}
&gt;             $maybe _ &lt;- muser
&gt;                 &lt;form method=post enctype=#{enctype}&gt;
&gt;                     ^{commentWidget}
&gt;                     &lt;div&gt;
&gt;                         &lt;input type=submit value=_{MsgAddCommentButton}&gt;
&gt;             $nothing
&gt;                 &lt;p&gt;
&gt;                     &lt;a href=@{AuthR LoginR}&gt;_{MsgLoginToComment}
&gt; |]

Receive an incoming comment submission.

&gt; postEntryR :: EntryId -&gt; Handler RepHtml
&gt; postEntryR entryId = do
&gt;     ((res, commentWidget), enctype) &lt;-
&gt;         runFormPost (commentForm entryId)
&gt;     case res of
&gt;         FormSuccess comment -&gt; do
&gt;             _ &lt;- runDB $ insert comment
&gt;             setMessageI MsgCommentAdded
&gt;             redirect $ EntryR entryId
&gt;         _ -&gt; defaultLayout $ do
&gt;             setTitleI MsgPleaseCorrectComment
&gt;             [whamlet|
&gt; &lt;form method=post enctype=#{enctype}&gt;
&gt;     ^{commentWidget}
&gt;     &lt;div&gt;
&gt;         &lt;input type=submit value=_{MsgAddCommentButton}&gt;
&gt; |]

Finally our main function.

&gt; main :: IO ()
&gt; main = do
&gt;     pool &lt;- createSqlitePool "blog.db3" 10 -- create a new pool
&gt;     -- perform any necessary migration
&gt;     runSqlPool (runMigration migrateAll) pool
&gt;     manager &lt;- newManager def -- create a new HTTP manager
&gt;     warpDebug 3000 $ Blog pool manager -- start our server</pre>
 </html>