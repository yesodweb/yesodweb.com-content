<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE concept PUBLIC "-//OASIS//DTD DITA Concept//EN" "concept.dtd">
<concept id="persistent-112"><title>Persistent</title><conbody><p id="112-i1">Forms deal with the boundary between the user and the application. Another boundary we
            need to deal with is between the application and the storage layer. Whether it be a SQL
            database, a YAML file, or a binary blob, odds are you have to work to get your storage
            layer to accept your application datatypes. Persistent is Yesod's answer to data
            storage- a type-safe, universal data store interface for Haskell. </p><p id="112-i1.5">Haskell has many different database bindings available. However, most of these have
            little knowledge of a schema and therefore do not provide useful static guarantees. They
            also force database-dependent APIs and data types on the programmer. Haskellers have
            attempted a more revolutionary route of creating Haskell specific data stores to get
            around these flaws that allow one to easily store any Haskell type. These options are
            great for certain use cases, but they constrain one to the storage techniques provided
            by the library, do not interface well with other languages, and the flexibility can also
            mean one must write reams of code for querying data. In contrast, Persistent allows us
            to choose among existing databases that are highly tuned for different data storage use
            cases, interoperate with other programming languages, and to use a safe and productive
            query interface. </p><p id="112-i2">Persistent follows the guiding principles of type safety and concise, declarative syntax. Some other nice features are:</p><ul id="112-x1"><li id="112-x2">Database-agnostic. There is first class support for PostgreSQL, SQLite and MongoDB, with
                experimental CouchDB and MySQL support in the works.</li><li id="112-x3">By being non-relational in nature, we simultaneously are able to support a wider number of storage layers and are not constrained by some of the performance bottlenecks incurred through joins.</li><li id="112-x4">A major source of frustration in dealing with SQL databases is changes to the schema. Persistent can automatically perform database migrations.</li></ul>
    <!-- still want to cover:
        
        Advanced: select enumerator interface (maybe)
        Join modules
    -->

</conbody><concept id="synopsis-113"><title>Synopsis</title><conbody>  <codeblock id="113-x1" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TemplateHaskell, TypeFamilies, OverloadedStrings #-}
{-# LANGUAGE GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    name String
    age Int Maybe
    deriving Show
BlogPost
    title String
    authorId PersonId
    deriving Show
|]

main :: IO ()
main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll

    johnId &lt;- insert $ Person "John Doe" $ Just 35
    janeId &lt;- insert $ Person "Jane Doe" Nothing

    insert $ BlogPost "My fr1st p0st" johnId
    insert $ BlogPost "One more for good measure" johnId

    oneJohnPost &lt;- selectList [BlogPostAuthorId ==. johnId] [LimitTo 1]
    liftIO $ print (oneJohnPost :: [Entity BlogPost])

    john &lt;- get johnId
    liftIO $ print (john :: Maybe Person)

    delete janeId
    deleteWhere [BlogPostAuthorId ==. johnId]</codeblock>
 </conbody></concept><concept id="solving-the-boundary-issue-114"><title>Solving the boundary issue</title><conbody><p id="114-b1">Suppose you are storing information on people in a SQL database. Your table might look
            something like:</p><codeblock outputclass="sql" id="114-x1">CREATE TABLE Person(id SERIAL PRIMARY KEY, name VARCHAR NOT NULL, age INTEGER)</codeblock><p id="114-b2">And if you are using a database like PostgreSQL, you can be guaranteed that the database will never store some arbitrary text in your age field. (The same cannot be said of SQLite, but let's forget about that for now.) To mirror this database table, you would likely create a Haskell datatype that looks something like:</p><codeblock id="114-x2" outputclass="haskell">data Person = Person
    { personName :: Text
    , personAge :: Int
    }</codeblock><p id="114-b3">It looks like everything is type safe: the database schema matches our Haskell datatypes, the database ensures that invalid data can never make it into our data store, and everything is generally awesome. Well, until:</p><ul id="114-x3"><li id="114-x4">You want to pull data from the database, and the database layer gives you the data in an untyped format.</li><li id="114-x5">You want to find everyone older than 32, and you accidently write "thirtytwo" in your SQL statement. Guess what: that will compile just fine, and you won't find out you have a problem until runtime.</li><li id="114-x6">You decide you want to find the first 10 people alphabetically. No problem... until you
                make a typo in your SQL. Once again, you don't find out until runtime.</li></ul><p id="114-b4">In dynamic languages, the answers to these issues is unit testing. For everything that <i id="114-x7">can</i> go wrong, make sure you write a test case. But as I am sure you are aware by now, that doesn't jive well with the Yesod approach to things. We like to take advantage of Haskell's strong typing to save us wherever possible, and data storage is no exception.</p><p id="114-b5">So the question remains: how can we use Haskell's type system to save the day?</p></conbody><concept id="types-115"><title>Types</title><conbody><p id="115-ba1">Like routing, there is nothing intrinsically difficult about type-safe data access. It
            just requires a lot of monotonous, error prone, boiler plate code. As usual, this means
            we can use the type system to keep us honest. And to avoid some of the drudgery, we'll
            use a sprinkling of Template Haskell.</p>
<note id="115-x1">Earlier versions of Persistent made much heavier usage of Template Haskell. Starting
            with 0.6, there is a new architecture inspired by the <apiname id="115-x2">groundhog</apiname>
            package. This approach uses phantom types to carry a lot of the burden.</note>
<p id="115-ba2"><codeph id="115-x3">PersistValue</codeph> is the basic building block of Persistent. It is a
            sum type that can represent data that gets sent to and from a database. Its definition
            is:</p><codeblock id="115-x4" outputclass="haskell">data PersistValue = PersistText Text
                  | PersistByteString ByteString
                  | PersistInt64 Int64
                  | PersistDouble Double
                  | PersistBool Bool
                  | PersistDay Day
                  | PersistTimeOfDay TimeOfDay
                  | PersistUTCTime UTCTime
                  | PersistNull
                  | PersistList [PersistValue]
                  | PersistMap [(T.Text, PersistValue)]
                  | PersistForeignKey ByteString -- ^ intended especially for MongoDB backend</codeblock><p id="115-ba4">Each Persistent backend needs to know how to translate the relevant values into
            something the database can understand. However, it would be awkward do have to express
            all of our data simply in terms of these basic types. The next layer is the <codeph id="115-x5">PersistField</codeph> typeclass, which defines how an arbitrary Haskell
            datatype can be marshaled to and from a <codeph id="115-x6">PersistValue</codeph>. A
                <codeph id="115-x-3">PersistField</codeph> correlates to a column in a SQL database. In our
            person example above, name and age would be our <codeph id="115-x7">PersistField</codeph>s.</p><p id="115-ba5">To tie up the user side of the code, our last typeclass is <codeph id="115-x8">PersistEntity</codeph>. An instance of PersistEntity correlates with a table in a
            SQL database. This typeclass defines a number of functions and some associated types. To
            review, we have the following correspondence between Persistent and SQL:</p>
        <simpletable id="115-simpletable_hmz_kck_kd">
            <sthead id="115-x9">
                <stentry id="115-x10">SQL</stentry>
                <stentry id="115-x11">Persistent</stentry>
            </sthead>
            <strow id="115-x12">
                <stentry id="115-x13">Datatypes (VARCHAR, INTEGER, etc)</stentry>
                <stentry id="115-x14">PersistValue</stentry>
            </strow>
            <strow id="115-x18">
                <stentry id="115-x19">Column</stentry>
                <stentry id="115-x20">PersistField</stentry>
            </strow>
            <strow id="115-x15">
                <stentry id="115-x16">Table</stentry>
                <stentry id="115-x17">PersistEntity</stentry>
            </strow>
        </simpletable></conbody></concept><concept id="code-generation-116"><title>Code Generation</title><conbody><p id="116-bb1">In order to ensure that the PersistEntity instances match up properly with your Haskell datatypes, Persistent takes responsibility for both. This is also good from a DRY (Don't Repeat Yourslef) perspective: you only need to define your entities once. Let's see a quick example:</p><codeblock id="116-x1" outputclass="haskell">-- START
{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell, OverloadedStrings, GADTs #-}
import Database.Persist
import Database.Persist.TH
import Database.Persist.Sqlite
import Control.Monad.IO.Class (liftIO)

mkPersist sqlSettings [persist|
Person
    name String
    age Int
    deriving Show
|]
-- STOP
main = return ()
</codeblock><p id="116-bb2">We use a combination of Template Haskell and Quasi-Quotation (like when defining
            routes): <apiname id="116-x6">persistent-template:Database.Persist.TH:persist</apiname> is a
            quasi-quoter which converts a whitespace-sensitive syntax into a list of entity
            definitions. (You can also declare your entities in a separate file using
                <apiname id="116-x9">persistent-template:Database.Persist.TH:persistFile</apiname>.)
                <apiname id="116-x10">persistent-template:Database.Persist.TH:mkPersist</apiname> takes that list
            of entities and declares:</p><ul id="116-x2"><li id="116-x3">One Haskell datatype for each entity.</li><li id="116-x4">A <codeph id="116-x-3">PersistEntity</codeph> instance for each datatype defined.</li></ul>
        <p>The example above generates code that looks like the following:</p>
        <codeblock outputclass="haskell">{-# OPTIONS_GHC -Wwarn #-}
-- START
{-# LANGUAGE TypeFamilies, GeneralizedNewtypeDeriving, OverloadedStrings, GADTs #-}
import Database.Persist
import Database.Persist.Store
import Database.Persist.Sqlite
import Database.Persist.EntityDef
import Control.Monad.IO.Class (liftIO)
import Control.Applicative

data Person = Person
    { personName :: String
    , personAge :: Int
    }
  deriving (Show, Read, Eq)

type PersonId = Key SqlPersist Person

instance PersistEntity Person where
    -- A Generalized Algebraic Datatype (GADT).
    -- This gives us a type-safe approach to matching fields with
    -- their datatypes.
    data EntityField Person typ where
        PersonId   :: EntityField Person PersonId
        PersonName :: EntityField Person String
        PersonAge  :: EntityField Person Int

    type PersistEntityBackend Person = SqlPersist

    toPersistFields (Person name age) =
        [ SomePersistField name
        , SomePersistField age
        ]

    fromPersistValues [nameValue, ageValue] = Person
        &lt;$&gt; fromPersistValue nameValue
        &lt;*&gt; fromPersistValue ageValue
    fromPersistValues _ = Left "Invalid fromPersistValues input"

    -- Information on each field, used internally to generate SQL statements
    persistFieldDef PersonId = FieldDef
        (HaskellName "Id")
        (DBName "id")
        (FTTypeCon Nothing "PersonId")
        []
    persistFieldDef PersonName = FieldDef
        (HaskellName "name")
        (DBName "name")
        (FTTypeCon Nothing "String")
        []
    persistFieldDef PersonAge = FieldDef
        (HaskellName "age")
        (DBName "age")
        (FTTypeCon Nothing "Int")
        []
-- STOP
    data Unique Person typ = IgnoreThis
    entityDef = undefined
    halfDefined = undefined
    persistUniqueToFieldNames = undefined
    persistUniqueToValues = undefined
    persistUniqueKeys = undefined
    persistIdField = undefined
main :: IO ()
main = return ()</codeblock><p id="116-bb3">As you might expect, our <codeph>Person</codeph> datatype closely matches the definition
            we gave in the original Template Haskell version. We also have a Generalized Algebraic
            Datatype (GADT) which gives a separate constructor for each field. This GADT encodes
            both the type of the entity and the type of the field. We use its constructors
            throughout Persistent, such as to ensure that when we apply a filter, the types of the
            filtering value match the field.</p>
        <p>We can use the generated <codeph>Person</codeph> type like any other Haskell type, and
            then pass it off to other Persistent functions.</p><codeblock id="116-x5" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell, OverloadedStrings, GADTs #-}
import Database.Persist
import Database.Persist.TH
import Database.Persist.Sqlite
import Control.Monad.IO.Class (liftIO)

mkPersist sqlSettings [persist|
Person
    name String
    age Int
    deriving Show
|]
-- START
main = withSqliteConn ":memory:" $ runSqlConn $ do
    michaelId &lt;- insert $ Person "Michael" 26
    michael &lt;- get michaelId
    liftIO $ print michael
-- STOP
</codeblock><p id="116-bb4">We start off with some standard database connection code. In this case, we used the single-connection functions. Persistent also comes built in with connection pool functions, which we will generally want to use in production.</p><p id="116-bb5">In this example, we have seen two functions: <codeph id="116-x-4">insert</codeph> creates a new
            record in the database and returns its ID. Like everything else in Persistent, IDs are
            type safe. We'll get into more details of how these IDs work later. So when you call
                <codeph id="116-x7">insert $ Person "Michael" 25</codeph>, it gives you a value back of
            type <codeph id="116-x8">PersonId</codeph>.</p>
<p id="116-bb6">The next function we see is <codeph id="116-x-5">get</codeph>, which attempts to load a value from
            the database using an <codeph id="116-x11">Id</codeph>. In Persistent, you never need to
            worry that you are using the key from the wrong table: trying to load up a different
            entity (like <codeph id="116-x-6">House</codeph>) using a <codeph id="116-x12">PersonId</codeph> will
            never compile.</p></conbody></concept><concept id="persistbackend-117"><title>PersistStore</title><conbody><p id="117-bc1">One last detail is left unexplained from the previous example: what are those
                <codeph id="117-x-3">withSqliteConn</codeph> and <codeph id="117-x-4">runSqlConn</codeph> functions doing, and
            what is that monad that our database actions are running in?</p><p id="117-bc2">All database actions need to occur within an instance of <codeph id="117-x1">PersistStore</codeph>. As its name implies, every data store (PostgreSQL, SQLite,
            MongoDB) has an instance of <codeph id="117-x-5">PersistStore</codeph>. This is where all the
            translations from <codeph id="117-x2">PersistValue</codeph> to database-specific values
            occur, where SQL query generation happens, and so on.</p><note id="117-x3" type="other" othertype="advanced"><p id="117-bc3">As you can imagine, even though <codeph id="117-x-6">PersistStore</codeph> provides a safe,
                well-typed interface to the outside world, there are a lot of database interactions
                that could go wrong. However, by testing this code automatically and thoroughly in a
                single location, we can centralize our error-prone code and make sure it is as
                bug-free as possible.</p></note><p id="117-bc4"><codeph id="117-x-7">withSqliteConn</codeph> creates a single connection to a database using its
            supplied connection string. For our test cases, we will use <codeph id="117-x-8">:memory:</codeph>,
            which uses an in-memory database. <codeph id="117-x-9">runSqlConn</codeph> uses that connection to
            run the inner action. Both SQLite and PostgreSQL share the same instance of
                <codeph id="117-x-10">PersistStore</codeph>: <codeph id="117-x-11">SqlPersist</codeph>.</p>
        <note id="117-x-12">There are actually a few other typeclasses: <codeph id="117-x-13">PersistUpdate</codeph> and
                <codeph id="117-x-14">PersistQuery</codeph>. Different typeclasses provide different
            functionality, which allows us to write backends that use simpler data stores (e.g.,
            Redis) even though they can't provide us all the high-level functionality available in
            Persistent.</note><p id="117-bc5">One important thing to note is that everything which occurs inside a single call to
                <codeph id="117-x-15">runSqlConn</codeph> runs in a single transaction. This has two important
            implications:</p><ul id="117-x4"><li id="117-x5">For many databases, committing a transaction can be a costly activity. By putting multiple steps into a single transaction, you can speed up code dramatically.</li><li id="117-x6">If an exception is thrown anywhere inside a single call to <codeph id="117-x-16">runSqlConn</codeph>,
                all actions will be rolled back (assuming your backend has rollback support).</li></ul></conbody></concept></concept><concept id="migrations-118"><title>Migrations</title><conbody><p id="118-m1">I'm sorry to tell you, but so far I have lied to you a bit: the example from the previous section does not actually work. If you try to run it, you will get an error message about a missing table.</p><p id="118-m2">For SQL databases, one of the major pains can be managing schema changes. Instead of leaving this to the user, Persistent steps in to help, but you have to <i id="118-x1">ask</i> it to help. Let's see what this looks like:</p><codeblock id="118-x2" outputclass="haskell">-- START
{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.TH
import Database.Persist.Sqlite
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkSave "entityDefs"] [persist|
Person
    name String
    age Int
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration $ migrate entityDefs (undefined :: Person) -- this line added: that's it!
    michaelId &lt;- insert $ Person "Michael" 26
    michael &lt;- get michaelId
    liftIO $ print michael
-- STOP
</codeblock><p id="118-m3">With this one little code change, Persistent will automatically create your
                <codeph id="118-x-3">Person</codeph> table for you. This split between
                <codeph id="118-x-4">runMigration</codeph> and <codeph id="118-x-5">migrate</codeph> allows you to migrate
            multiple tables simultaneously.</p><p id="118-m4">This works when dealing with just a few entities, but can quickly get tiresome once we
            are dealing with a dozen entities. Instead of repeating yourself, Persistent provides a
            helper function, <codeph id="118-x-6">mkMigrate</codeph>:</p><codeblock id="118-x3" outputclass="haskell">-- START
{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    name String
    age Int
    deriving Show
Car
    color String
    make String
    model String
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll
-- STOP
</codeblock><p id="118-m5"><codeph id="118-x-7">mkMigrate</codeph> is a Template Haskell function which creates a new function
            that will automatically call <codeph id="118-x-8">migrate</codeph> on all entities defined in the
                <codeph id="118-x-9">persist</codeph> block. The <codeph id="118-x-10">share</codeph> function is just a little
            helper that passes the information from the persist block to each Template Haskell
            function and concatenates the results.</p><p id="118-m6">Persistent has very conservative rules about what it will do during a migration. It
            starts by loading up table information from the database, complete with all defined SQL
            datatypes. It then compares that against the entity definition given in the code. For
            the following cases, it will automatically alter the schema:</p><ul id="118-x4"><li id="118-x5">The datatype of a field changed. However, the database may object to this modification if the data cannot be translated.</li><li id="118-x6">A field was added. However, if the field is not null, no default value is supplied (we'll discuss defaults later) and there is already data in the database, the database will not allow this to happen.</li><li id="118-x7">A field is converted from not null to null. In the opposite case, Persistent will attempt the conversion, contingent upon the database's approval.</li><li id="118-x8">A brand new entity is added.</li></ul><p id="118-m7">However, there are some cases that Persistent will not handle:</p><ul id="118-x9"><li id="118-x10">Field or entity renames: Persistent has no way of knowing that "name" has now been renamed to "fullName": all it sees is an old field called name and a new field called fullName.</li><li id="118-x11">Field removals: since this can result in data loss, Persistent by default will refuse
                to perform the action (you can force the issue by using
                    <codeph id="118-x-11">runMigrationUnsafe</codeph> instead of <codeph id="118-x-12">runMigration</codeph>,
                though it is <b id="118-x12">not</b> recommended).</li></ul><p id="118-m8"><codeph id="118-x-13">runMigration</codeph> will print out the migrations it is running on
                <codeph id="118-x-14">stderr</codeph> (you can bypass this by using
                <codeph id="118-x-15">runMigrationSilent</codeph>). Whenever possible, it uses <codeph id="118-x-16">ALTER
                TABLE</codeph> calls. However, in SQLite, <codeph id="118-x-17">ALTER TABLE</codeph> has very
            limited abilities, and therefore Persistent must resort to copying the data from one
            table to another.</p><p id="118-m9">Finally, if instead of <i id="118-x13">performing</i> a migration, you want Persistent to
            give you hints about what migrations are necessary, use the
                <codeph id="118-x-18">printMigration</codeph> function. This function will print out the
            migrations which <codeph id="118-x-19">runMigration</codeph> would perform for you. This may be useful
            for performing migrations that Persistent is not capable of, for adding arbitrary SQL to
            a migration, or just to log what migrations occurred.</p></conbody></concept><concept id="uniqueness-119"><title>Uniqueness</title><conbody><p id="119-u1">In addition to declaring fields within an entity, you can also declare uniqueness
            constraints. A typical example would be requiring that a username be unique.</p>
        <fig id="119-x1">
            <title id="119-x4">Unique Username</title>
            <codeblock outputclass="entities" id="119-x5">User
    username Text
    UniqueUsername username</codeblock>
        </fig>
        <p id="119-x6">While each field name must begin with a lowercase letter, the uniqueness constraints must
            begin with an uppercase letter.</p><codeblock id="119-x2" outputclass="haskell">-- START
{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    firstName String
    lastName String
    age Int
    PersonName firstName lastName
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll
    insert $ Person "Michael" "Snoyman" 26
    michael &lt;- getBy $ PersonName "Michael" "Snoyman"
    liftIO $ print michael
-- STOP</codeblock><p id="119-u2">To declare a unique combination of fields, we add an extra line to our declaration. Persistent knows that it is defining a unique constructor, since the line begins with a capital letter. Each following word must be a field in this entity.</p><p id="119-u3">The main restriction on uniqueness is that it can only be applied non-null fields. The
            reason for this is that the SQL standard is ambiguous on how uniqueness should be
            applied to <codeph id="119-x-3">NULL</codeph> (e.g., is <codeph id="119-x-4">NULL=NULL</codeph> true or false?).
            Besides that ambiguity, most SQL engines in fact implement rules which would be <i id="119-x3">contrary</i> to what the Haskell datatypes anticipate (e.g., PostgreSQL says
            that <codeph id="119-x-5">NULL=NULL</codeph> is false, whereas Haskell says <codeph id="119-x-6">Nothing ==
                Nothing</codeph> is <codeph id="119-x-7">True</codeph>).</p>
        <p id="119-x7">In addition to providing nice guarantees at the database level about consistency
            of your data, uniqueness constraints can also be used to perform some specific queries
            within your Haskell code, like the <codeph id="119-x-8">getBy</codeph> demonstrated above.
            This happens via the <codeph id="119-x-9">Unique</codeph> associated type. In the example
            above, we end up with a new constructor:</p>
        <codeblock outputclass="haskell" id="119-x8">PersonName :: String -&gt; String -&gt; Unique Person</codeblock></conbody></concept><concept id="queries-120"><title>Queries</title><conbody>
        <p id="120-x-3">Depending on what your goal is, there are different approaches to querying the database.
            Some commands query based on a numeric ID, while others will filter. Queries also differ
            in the number of results they return: some lookups should return no more than one result
            (if the lookup key is unique) while others can return many results.</p>
        <p id="120-x-4">Persistent therefore provides a few different query functions. As usual, we try to encode
            as many invariants in the types as possible. For example, a query that can return only 0
            or 1 results will use a <codeph id="120-x-5">Maybe</codeph> wrapper, whereas a query returning many
            results will return a list.</p>
    </conbody><concept id="fetching-by-id-121"><title>Fetching by ID</title><conbody>
  <p id="121-x1">The simplest query you can perform in Persistent is getting based on an ID. Since this
   value may or may not exist, its return type is wrapped in a <codeph id="121-x-3">Maybe</codeph>.</p>
  <fig id="121-x2">
   <title id="121-x3">Using get</title>
   <codeblock id="121-x4" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    firstName String
    lastName String
    age Int Gt Desc
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll
-- START
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    maybePerson &lt;- get personId
    case maybePerson of
        Nothing -&gt; liftIO $ putStrLn "Just kidding, not really there"
        Just person -&gt; liftIO $ print person
-- STOP</codeblock>
  </fig>
  <p id="121-x5">This can be very useful for sites that provide URLs like <filepath id="121-x6">/person/5</filepath>. However, in such a case, we don't usually care about the
    <codeph id="121-x-4">Maybe</codeph> wrapper, and just want the value, returning a 404 message if it is not
   found. Fortunately, the <apiname id="121-x7">yesod-persistent:Yesod.Persist:get404</apiname> function
   helps us out here. We'll go into more details when we see integration with Yesod.</p>
 </conbody></concept><concept id="fetching-by-unique-constraint-122"><title>Fetching by unique constraint</title><conbody>
  <p id="122-x1"><codeph id="122-x-3">getBy</codeph> is almost identical to <codeph id="122-x-4">get</codeph>, except it takes a
            uniqueness constraint instead of an ID it takes a Unique value.</p>
  <fig id="122-x2">
   <title id="122-x3">Using getBy</title>
   <codeblock id="122-x4" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    firstName String
    lastName String
    age Int Gt Desc
    UniqueName firstName lastName
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigrationSilent migrateAll
-- START
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    maybePerson &lt;- getBy $ UniqueName "Michael" "Snoyman"
    case maybePerson of
        Nothing -&gt; liftIO $ putStrLn "Just kidding, not really there"
        Just person -&gt; liftIO $ print person
-- STOP</codeblock>
  </fig>
        <p id="122-x-5">Like <codeph id="122-x-6">get404</codeph>, there is also a <codeph id="122-x-7">getBy404</codeph> function.</p>
 </conbody></concept><concept id="select-functions-123"><title>Select functions</title><conbody>
  <p id="123-x1">Most likely, you're going to want more powerful queries. You'll want to find everyone
   over a certain age; all cars available in blue; all users without a registered email address. For
   this, you need one of the select functions.</p>
  <p id="123-x2">All the select functions use a similar interface, with slightly different outputs:</p>
  <simpletable id="123-simpletable_wxh_wgw_kd">
   <sthead id="123-x3">
    <stentry id="123-x4">Function</stentry>
    <stentry id="123-x5">Returns</stentry>
   </sthead>
   <strow id="123-x6">
    <stentry id="123-x7">selectSource</stentry>
    <stentry id="123-x8">A <codeph id="123-x-3">Source</codeph> containing all the IDs and values from the database.
     This allows you to write streaming code.<note id="123-x-4">We cover <codeph id="123-x-5">Source</codeph>s in detail in
      the <xref id="123-x-6" href="conduits.dita#conduits-208" format="dita">conduits appendix</xref>.
      Additionally, there's another function called <codeph id="123-x-7">selectSourceConn</codeph> which allows
      you more control of connection allocation. We use this in the <xref id="123-x-8" href="case-study-sphinx.dita#case-study-sphinx-193" format="dita">Sphinx case
       study</xref>.</note></stentry>
   </strow>
   <strow id="123-x9">
    <stentry id="123-x10">selectList</stentry>
    <stentry id="123-x11">A list containing all the IDs and values from the database. All records will
     be loaded into memory.</stentry>
   </strow>
   <strow id="123-x12">
    <stentry id="123-x13">selectFirst</stentry>
    <stentry id="123-x14">Takes just the first ID and value from the database, if available</stentry>
   </strow>
   <strow id="123-x15">
    <stentry id="123-x16">selectKeys</stentry>
    <stentry id="123-x17">Returns only the keys, without the values, as a
     <codeph id="123-x-9">Source</codeph>.</stentry>
   </strow>
  </simpletable>
  <p id="123-x18"><codeph id="123-x-10">selectList</codeph> is the most commonly used, so we will cover it
   specifically. Understanding the others should be trivial after that.</p>
  <p id="123-x19"><codeph id="123-x-11">selectList</codeph> takes two arguments: a list of <codeph id="123-x-12">Filter</codeph>s,
   and a list of <codeph id="123-x-13">SelectOpt</codeph>s. The former is what limits your results based on
   characteristics; it allows for equals, less than, is member of, and such.
    <codeph id="123-x-14">SelectOpt</codeph>s provides for three different features: sorting, limiting output to a
   certain number of rows, and offsetting results by a certain number of rows.</p>
  <note id="123-x20">The combination of limits and offsets is very important; it allows for efficient
   pagination in your webapps.</note>
  <p id="123-x21">Let's jump straight into an example of filtering, and then analyze it.</p>
   <codeblock id="123-x22" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    firstName String
    lastName String
    age Int Gt Desc
    UniqueName firstName lastName
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigrationSilent migrateAll
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
-- START
    people &lt;- selectList [PersonAge &gt;. 25, PersonAge &lt;=. 30] []
    liftIO $ print people
-- STOP</codeblock>
  <p id="123-x23">As simple as that example is, we really need to cover three points:</p>
  <ol id="123-ol_zxf_yhw_kd">
   <li id="123-x24"><codeph id="123-x-15">PersonAge</codeph> is a constructor for an associated phantom type. That
    might sound scary, but what's important is that it uniquely identifies the "age" column of the
    "person" table, and that it knows that the age field is an <codeph id="123-x-16">Int</codeph>. (That's the
    phantom part.)</li>
   <li id="123-x25">We have a bunch of Persistent filtering operators. They're all pretty straight-forward: just
    tack a period to the end of what you'd expect. There are three gotchas here, I'll explain
    below.</li>
   <li id="123-x26">The list of filters is ANDed together, so that our constraint means "age is greater than 25
    AND age is less than or equal to 30". We'll describe ORing later.</li>
  </ol>
  <p id="123-x27">The one operator that's surprisingly named is "not equals." We use
    <codeph id="123-x-17">!=.</codeph>, since <codeph id="123-x-18">/=.</codeph> is used for updates (for "divide-and-set",
   described later). Don't worry: if you use the wrong one, the compiler will catch you. The other
   two surprising operators are the "is member" and "is not member". They are, respectively, <codeph id="123-x28">&lt;-.</codeph> and <codeph id="123-x29">/&lt;-.</codeph> (both end with a period).</p>
  <p id="123-x30">And regarding ORs, we use the <codeph id="123-x31">||.</codeph> operator. For example:</p>
  <codeblock id="123-x32" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    firstName String
    lastName String
    age Int Gt Desc
    UniqueName firstName lastName
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigrationSilent migrateAll
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
-- START
    people &lt;- selectList
        (       [PersonAge &gt;. 25, PersonAge &lt;=. 30]
            ||. [PersonFirstName /&lt;-. ["Adam", "Bonny"]]
            ||. ([PersonAge ==. 50] ||. [PersonAge ==. 60])
        )
        []
    liftIO $ print people
-- STOP</codeblock>
  <p id="123-x33">This (completely nonsensical) example means: find people who are 26-30, inclusive, OR whose
   names are neither Adam or Bonny, OR whose age is either 50 or 60.</p>
 </conbody><concept id="selectopt-124"><title>SelectOpt</title><conbody>
  <p id="124-x1">All of our <codeph id="124-x-3">selectList</codeph> calls have included an empty list as the second
   parameter. That specifies no options, meaning: sort however the database wants, return all
   results, and don't skip any results. A <codeph id="124-x-4">SelectOpt</codeph> has four constructors that can
   be used to change all that.</p>
  <dl id="124-x2">
   <dlentry id="124-x3">
    <dt id="124-x4">Asc</dt>
    <dd id="124-x5">Sort by the given column in ascending order. This uses the same phantom type as
     filtering, such as <codeph id="124-x-5">PersonAge</codeph>.</dd>
   </dlentry>
   <dlentry id="124-x6">
    <dt id="124-x7">Desc</dt>
    <dd id="124-x8">Same as <codeph id="124-x-6">Asc</codeph>, in descending order.</dd>
   </dlentry>
   <dlentry id="124-x9">
    <dt id="124-x10">LimitTo</dt>
    <dd id="124-x11">Takes an <codeph id="124-x-7">Int</codeph> argument. Only return up to the specified number of
     results.</dd>
   </dlentry>
   <dlentry id="124-x12">
    <dt id="124-x13">OffsetBy</dt>
    <dd id="124-x14">Takes an <codeph id="124-x-8">Int</codeph> argument. Skip the specified number of results.</dd>
   </dlentry>
  </dl>
  <p id="124-x15">The following code defines a function that will break down results into pages. It returns all
   people aged 18 and over, and then sorts them by age (oldest person first). For people with the
   same age, they are sorted alphabetically by last name, then first name.</p>
  <codeblock id="124-x16" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    firstName String
    lastName String
    age Int Gt Desc
    UniqueName firstName lastName
    deriving Show
|]

-- START
resultsForPage pageNumber = do
    let resultsPerPage = 10
    selectList
        [ PersonAge &gt;=. 18
        ]
        [ Desc PersonAge
        , Asc PersonLastName
        , Asc PersonFirstName
        , LimitTo resultsPerPage
        , OffsetBy $ (pageNumber - 1) * resultsPerPage
        ]
-- STOP

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigrationSilent migrateAll
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    resultsForPage 1 &gt;&gt;= liftIO . print</codeblock>
 </conbody></concept></concept></concept><concept id="manipulation-125"><title>Manipulation</title><conbody>
        <p id="125-x-3">Querying is only half the battle. We also need to be able to add data to and modify
            existing data in the database.</p>
    </conbody><concept id="insert-126"><title>Insert</title><conbody>  <p id="126-x1">It's all well and good to be able to play with data in the database, but how does it
   get there in the first place? The answer is the <codeph id="126-x-3">insert</codeph> function. You just give
   it a value, and it gives back an ID.</p>
  <p id="126-x2">At this point, it makes sense to explain a bit of the philosophy behind Persistent. In
   many other ORM solutions, the datatypes used to hold data are opaque: you need to go through
   their defined interfaces to get at and modify the data. That's not the case with Persistent:
   we're using plain old Algebraic Data Types for the whole thing. This means you still get all the
   great benefits of pattern matching, currying and everything else you're used to.</p>
  <p id="126-x3">However, there are a few things we <i id="126-x4">can't</i> do. For one, there's no way to automatically
   update values in the database every time the record is updated in Haskell. Of course, with
   Haskell's normal stance of purity and immutability, this wouldn't make much sense anyway, so I
   don't shed any tears over it.</p>
  <p id="126-x5">However, there is one issue that newcomers are often bothered by: why are IDs and values
   completely separate? It seems like it would be very logical to embed the ID inside the value. In
   other words, instead of
   having:<codeblock outputclass="haskell" id="126-x6">data Person = Person { name :: String }</codeblock>have<codeblock outputclass="haskell" id="126-x7">data Person = Person { personId :: PersonId, name :: String }</codeblock></p>
  <p id="126-x8">Well, there's one problem with this right off the bat: how do we do an
    <codeph id="126-x-4">insert</codeph>? If a Person needs to have an ID, and we get the ID by inserting, and an
   insert needs a Person, we have an impossible loop. We could solve this with
    <codeph id="126-x-5">undefined</codeph>, but that's just asking for trouble.</p>
  <p id="126-x9">OK, you say, let's try something a bit
   safer:<codeblock outputclass="haskell" id="126-x10">data Person = Person { personId :: Maybe PersonId, name :: String }</codeblock>I
   definitely prefer <codeph id="126-x11">insert $ Person Nothing "Michael"</codeph> to <codeph id="126-x12">insert $ Person undefined "Michael"</codeph>. And now our types will be much simpler, right?
   For example, <codeph id="126-x-6">selectList</codeph> could return a simple <codeph id="126-x13">[Person]</codeph>
   instead of that ugly <codeph id="126-x14">[Entity SqlPersist Person]</codeph>.</p>
  <note id="126-x-7"><codeph id="126-x-8">Entity</codeph> is a datatype that ties together both the ID and value of an entity.
   Since IDs can be different based on backend, we also need to provide the Persistent backend we're
   using. The datatype <codeph id="126-x-9">Entity SqlPersist Person</codeph> can be read as "the ID and value of
   a person stored in a SQL database."</note>
  <p id="126-x15">The problem is that the "ugliness" is incredibly useful. Having <codeph id="126-x16">Entity SqlPersist Person</codeph> makes it obvious, at the type level, that we're dealing with
   a value that exists in the database. Let's say we want to create a link to another page that
   requires the <codeph id="126-x17">PersonId</codeph> (not an uncommon occurrence as we'll discuss
   later). The <codeph id="126-x-10">Entity SqlPersist Person</codeph> form gives us unambiguous access to that
   information; embedding <codeph id="126-x-11">PersonId</codeph> within <codeph id="126-x-12">Person</codeph> with a
    <codeph id="126-x-13">Maybe</codeph> wrapper means an extra runtime check for <codeph id="126-x-14">Just</codeph>, instead
   of a more error-proof compile time check.</p>
  <p id="126-x18">Finally, there's a semantic mismatch with embedding the ID within the value. The
    <codeph id="126-x-15">Person</codeph> is the value. Two people are identical (in the context of a
   database) if all their fields are the same. By embedding the ID in the value, we're no longer
   talking about a person, but about a row in the database. Equality is no longer really equality,
   it's identity: is this the <i id="126-x19">same person</i>, as opposed to an equivalent person.</p>
  <p id="126-x20">In other words, there are some annoyances with having the ID separated out, but overall, it's
   the <i id="126-x21">right</i> approach, which in the grand scheme of things leads to better, less buggy
   code.</p>
 </conbody></concept><concept id="update-127"><title>Update</title><conbody>  <p id="127-x1">Now, in the context of that discussion, let's think about updating. The simplest way to
            update
            is:<codeblock id="127-x2" outputclass="haskell">let michael = Person "Michael" 26
    michaelAfterBirthday = michael { personAge = 27 }</codeblock>But
            that's not actually updating anything, it's just creating a new <codeph id="127-x-3">Person</codeph>
            value based on the old one. When we say update, we're <i id="127-x3">not</i> talking about
            modifications to the values in Haskell. (We better not be of course, since Haskell data
            types are immutable.)</p>
  <p id="127-x4">Instead, we're looking at ways of modifying rows in a table. And the simplest way to do
            that is with the <codeph id="127-x-4">update</codeph> function.</p>
  <codeblock id="127-x5" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person sql=the-person-table
    firstName String sql=first_name
    lastName String sql=fldLastName
    age Int Gt Desc "sql=The Age of the Person"
    UniqueName firstName lastName
    deriving Show
|]

resultsForPage pageNumber = do
    let resultsPerPage = 10
    selectList
        [ PersonAge &gt;=. 18
        ]
        [ Desc PersonAge
        , Asc PersonLastName
        , Asc PersonFirstName
        , LimitTo resultsPerPage
        , OffsetBy $ (pageNumber - 1) * resultsPerPage
        ]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigrationSilent migrateAll
-- START
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    update personId [PersonAge =. 27]
-- STOP
    resultsForPage 1 &gt;&gt;= liftIO . print</codeblock>
  <p id="127-x6"><codeph id="127-x-5">update</codeph> takes two arguments: an ID, and a list of <codeph id="127-x-6">Update</codeph>s. The simplest update is assignment, but it's not always
            the best. What if you want to increase someone's age by 1, but you don't have their
            current age? Persistent has you covered:</p>
  <codeblock id="127-x7" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person sql=the-person-table
    firstName String sql=first_name
    lastName String sql=fldLastName
    age Int Gt Desc "sql=The Age of the Person"
    UniqueName firstName lastName
    deriving Show
|]

resultsForPage pageNumber = do
    let resultsPerPage = 10
    selectList
        [ PersonAge &gt;=. 18
        ]
        [ Desc PersonAge
        , Asc PersonLastName
        , Asc PersonFirstName
        , LimitTo resultsPerPage
        , OffsetBy $ (pageNumber - 1) * resultsPerPage
        ]

-- START
haveBirthday personId = update personId [PersonAge +=. 1]
-- STOP

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigrationSilent migrateAll
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    update personId [PersonAge =. 27]
    haveBirthday personId
    resultsForPage 1 &gt;&gt;= liftIO . print</codeblock>
  <p id="127-x8">And as you might expect, we have all the basic mathematical operators:
                <codeph id="127-x-7">+=.</codeph>, <codeph id="127-x-8">-=.</codeph>, <codeph id="127-x-9">*=.</codeph>, and
                <codeph id="127-x-10">/=.</codeph> (full stop). These can be convenient for updating a single
            record, but they are also essential for proper ACID guarantees. Imagine the alternative:
            pull out a <codeph id="127-x-11">Person</codeph>, increment the age, and update the new value. If you
            have two threads/processes working on this database at the same time, you're in for a
            world of hurt (hint: race conditions).</p>
  <p id="127-x9">Sometimes you'll want to update many fields at once (give all your employees a 5% pay
            increase, for example). <codeph id="127-x-12">updateWhere</codeph> takes two parameters: a list of
            filters, and a list of updates to apply.</p>
  <codeblock id="127-x10" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person sql=the-person-table
    firstName String sql=first_name
    lastName String sql=fldLastName
    age Int Gt Desc "sql=The Age of the Person"
    UniqueName firstName lastName
    deriving Show
|]

resultsForPage pageNumber = do
    let resultsPerPage = 10
    selectList
        [ PersonAge &gt;=. 18
        ]
        [ Desc PersonAge
        , Asc PersonLastName
        , Asc PersonFirstName
        , LimitTo resultsPerPage
        , OffsetBy $ (pageNumber - 1) * resultsPerPage
        ]

haveBirthday personId = update personId [PersonAge +=. 1]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigrationSilent migrateAll
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    update personId [PersonAge =. 27]
    haveBirthday personId
    resultsForPage 1 &gt;&gt;= liftIO . print
-- START
    updateWhere [PersonFirstName ==. "Michael"] [PersonAge *=. 2] -- it's been a long day
-- STOP</codeblock>
  <p id="127-x11">Occassionally, you'll just want to completely replace the value in a database with a
            different value. For that, you use (surprise) the <codeph id="127-x-13">replace</codeph> function.</p>
  <codeblock id="127-x12" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person sql=the-person-table
    firstName String sql=first_name
    lastName String sql=fldLastName
    age Int Gt Desc "sql=The Age of the Person"
    UniqueName firstName lastName
    deriving Show
|]

resultsForPage pageNumber = do
    let resultsPerPage = 10
    selectList
        [ PersonAge &gt;=. 18
        ]
        [ Desc PersonAge
        , Asc PersonLastName
        , Asc PersonFirstName
        , LimitTo resultsPerPage
        , OffsetBy $ (pageNumber - 1) * resultsPerPage
        ]

haveBirthday personId = update personId [PersonAge +=. 1]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigrationSilent migrateAll
-- START
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    replace personId $ Person "John" "Doe" 20
-- STOP
    update personId [PersonAge =. 27]
    haveBirthday personId
    updateWhere [PersonFirstName ==. "Michael"] [PersonAge *=. 2] -- it's been a long day
    resultsForPage 1 &gt;&gt;= liftIO . print</codeblock>
</conbody></concept><concept id="delete-128"><title>Delete</title><conbody>
  <p id="128-x1">As much as it pains us, sometimes we must part with our data. To do so, we have three
   functions:</p>
  <dl id="128-x2">
   <dlentry id="128-x3">
    <dt id="128-x4">delete</dt>
    <dd id="128-x5">Delete based on an ID</dd>
   </dlentry>
   <dlentry id="128-x6">
    <dt id="128-x7">deleteBy</dt>
    <dd id="128-x8">Delete based on a unique constraint</dd>
   </dlentry>
   <dlentry id="128-x9">
    <dt id="128-x10">deleteWhere</dt>
    <dd id="128-x11">Delete based on a set of filters</dd>
   </dlentry>
  </dl>
  <codeblock id="128-x12" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person sql=the-person-table
    firstName String sql=first_name
    lastName String sql=fldLastName
    age Int Gt Desc "sql=The Age of the Person"
    UniqueName firstName lastName
    deriving Show
|]

resultsForPage pageNumber = do
    let resultsPerPage = 10
    selectList
        [ PersonAge &gt;=. 18
        ]
        [ Desc PersonAge
        , Asc PersonLastName
        , Asc PersonFirstName
        , LimitTo resultsPerPage
        , OffsetBy $ (pageNumber - 1) * resultsPerPage
        ]

haveBirthday personId = update personId [PersonAge +=. 1]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigrationSilent migrateAll
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    replace personId $ Person "John" "Doe" 20
    update personId [PersonAge =. 27]
    haveBirthday personId
    updateWhere [PersonFirstName ==. "Michael"] [PersonAge *=. 2] -- it's been a long day
    resultsForPage 1 &gt;&gt;= liftIO . print
-- START
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    delete personId
    deleteBy $ UniqueName "Michael" "Snoyman"
    deleteWhere [PersonFirstName ==. "Michael"]
-- STOP</codeblock>
  <p id="128-x13">We can even use deleteWhere to wipe out all the records in a table, we just need to give some
   hints to GHC as to what table we're interested in:</p>
  <codeblock id="128-x14" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person sql=the-person-table
    firstName String sql=first_name
    lastName String sql=fldLastName
    age Int Gt Desc "sql=The Age of the Person"
    UniqueName firstName lastName
    deriving Show
|]

resultsForPage pageNumber = do
    let resultsPerPage = 10
    selectList
        [ PersonAge &gt;=. 18
        ]
        [ Desc PersonAge
        , Asc PersonLastName
        , Asc PersonFirstName
        , LimitTo resultsPerPage
        , OffsetBy $ (pageNumber - 1) * resultsPerPage
        ]

haveBirthday personId = update personId [PersonAge +=. 1]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigrationSilent migrateAll
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    replace personId $ Person "John" "Doe" 20
    update personId [PersonAge =. 27]
    haveBirthday personId
    updateWhere [PersonFirstName ==. "Michael"] [PersonAge *=. 2] -- it's been a long day
    resultsForPage 1 &gt;&gt;= liftIO . print
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    delete personId
    deleteBy $ UniqueName "Michael" "Snoyman"
    deleteWhere [PersonFirstName ==. "Michael"]
-- START
    deleteWhere ([] :: [Filter Person])
-- STOP</codeblock>
 </conbody></concept></concept><concept id="attributes-129"><title>Attributes</title><conbody><p id="129-attr1">So far, we have seen a basic syntax for our <codeph id="129-x-3">persist</codeph> blocks: a line
            for the name of our entities, and then an indented line for each field with two words:
            the name of the field and the datatype of the field. Persistent handles more than this:
            you can assign an arbitrary list of attributes after the first two words on a line.</p><p id="129-attr2">Suppose we want to have a <codeph id="129-x-4">Person</codeph> entity with an (optional) age and
            the timestamp of when he/she was added to the system. For entities already in the
            database, we want to just use the current date-time for that timestamp.</p><codeblock id="129-x1" outputclass="haskell">-- START
{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    name String
    age Int Maybe
    created UTCTime default=now()
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    time &lt;- liftIO getCurrentTime
    runMigration migrateAll
    insert $ Person "Michael" (Just 26) time
    insert $ Person "Greg" Nothing time
-- STOP
</codeblock><p id="129-attr3"><codeph id="129-x2">Maybe</codeph> is a built in, single word attribute. It makes the
            field optional. In Haskell, this means it is wrapped in a <codeph id="129-x-5">Maybe</codeph>. In
            SQL, it makes the column nullable.</p>
        <p id="129-x8">The <codeph id="129-x3">default</codeph> attribute is backend specific, and uses
            whatever syntax is understood by the database. In this case, it uses the database's
            built-in <codeph id="129-x-6">now()</codeph> function. Suppose that we now want to add a
            field for a person's favorite programming language:</p><codeblock id="129-x4" outputclass="haskell">-- START
{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    name String
    age Int Maybe
    created UTCTime default=now()
    language String default='Haskell'
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll
-- STOP
</codeblock>
<note id="129-x5">The <codeph id="129-x6">default</codeph> attribute has absolutely no impact on the Haskell code itself; you still need to fill in all values. This will only affect the database schema and automatic migrations.</note>
<p id="129-attr4">We need to surround the string with single quotes so that the database can properly
            interpret it. Finally, Persistent can use double quotes for containing white space, so
            if we want to set someone's default home country to be El Salvador:</p><codeblock id="129-x7" outputclass="haskell">-- START
{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    name String
    age Int Maybe
    created UTCTime default=now()
    language String default='Haskell'
    country String "default='El Salvador'"
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll
-- STOP</codeblock>
        <p id="129-x9">One last trick you can do with attributes is to specify the names to be used for
            the SQL tables and columns. This can be convenient when interacting with existing
            databases.</p>
        <codeblock id="129-x10" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time
import Control.Monad.IO.Class (liftIO)

-- START
share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person sql=the-person-table
    firstName String sql=first_name
    lastName String sql=fldLastName
    age Int Gt Desc "sql=The Age of the Person"
    UniqueName firstName lastName
    deriving Show
|]
-- STOP

resultsForPage pageNumber = do
    let resultsPerPage = 10
    selectList
        [ PersonAge &gt;=. 18
        ]
        [ Desc PersonAge
        , Asc PersonLastName
        , Asc PersonFirstName
        , LimitTo resultsPerPage
        , OffsetBy $ (pageNumber - 1) * resultsPerPage
        ]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll
    personId &lt;- insert $ Person "Michael" "Snoyman" 26
    resultsForPage 1 &gt;&gt;= liftIO . print</codeblock></conbody></concept><concept id="relations-130"><title>Relations</title><conbody><p id="130-r1">Persistent allows references between your data types in a manner that is consistent with
            supporting non-SQL databases. We do this by embedding an ID in the related entity. So if
            a person has many cars:</p><codeblock id="130-x1" outputclass="haskell">-- START
{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Control.Monad.IO.Class (liftIO)
import Data.Time

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    name String
    deriving Show
Car
    ownerId PersonId Eq
    name String
    deriving Show
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll
    bruce &lt;- insert $ Person "Bruce Wayne"
    insert $ Car bruce "Bat Mobile"
    insert $ Car bruce "Porsche"
    -- this could go on a while
    cars &lt;- selectList [CarOwnerId ==. bruce] []
    liftIO $ print cars
-- STOP
</codeblock><p id="130-r4">Using this technique, you can define one-to-many relationships. To define many-to-many
            relationships, we need a join entity, which has a one-to-many relationship with each of
            the original tables. It is also a good idea to use uniqueness constraints on these. For
            example, to model a situation where we want to track which people have shopped in which
            stores:</p><codeblock id="130-x2" outputclass="haskell">-- START
{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH
import Data.Time

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    name String
Store
    name String
PersonStore
    personId PersonId
    storeId StoreId
    UniquePersonStore personId storeId
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll

    bruce &lt;- insert $ Person "Bruce Wayne"
    michael &lt;- insert $ Person "Michael"

    target &lt;- insert $ Store "Target"
    gucci &lt;- insert $ Store "Gucci"
    sevenEleven &lt;- insert $ Store "7-11"

    insert $ PersonStore bruce gucci
    insert $ PersonStore bruce sevenEleven

    insert $ PersonStore michael target
    insert $ PersonStore michael sevenEleven
-- STOP</codeblock></conbody></concept><concept id="closer-look-at-types-131"><title>Closer look at types</title><conbody>
  <p id="131-x1">So far, we've spoken about <codeph id="131-x-3">Person</codeph> and <codeph id="131-x-4">PersonId</codeph>
   without really explaining what they are. In the simplest sense, for a SQL-only system, the
    <codeph id="131-x-5">PersonId</codeph> could just be <codeph id="131-x2">type PersonId = Int64</codeph>. However,
   that means there is nothing binding a <codeph id="131-x-6">PersonId</codeph> at the type level to the
    <codeph id="131-x-7">Person</codeph> entity. As a result, you could accidently use a
    <codeph id="131-x-8">PersonId</codeph> and get a <codeph id="131-x-9">Car</codeph>. In order to model this relationship,
   we use phantom types. So, our next naive step would be:</p>
  <codeblock id="131-x3" outputclass="haskell">newtype Key entity = Key Int64
type PersonId = Key Person</codeblock>
  <note id="131-x4">Prior to Persistent 0.6, we used associated types instead of phantom types. You could solve
   the problem in that direction as well, but phantoms worked out better.</note>
  <p id="131-x5">And that works out really well, until you get to a backend that doesn't use Int64 for
   its IDs. And that's not just a theoretical question; MongoDB uses <codeph id="131-x-10">ByteString</codeph>s
   instead. So what we need is a key value that can contain an <codeph id="131-x-11">Int</codeph> and a
    <codeph id="131-x-12">ByteString</codeph>. Seems like a great time for a sum type:</p>
  <codeblock id="131-x6" outputclass="haskell">data Key entity = KeyInt Int64 | KeyByteString ByteString</codeblock>
  <p id="131-x7">But that's just asking for trouble. Next we'll have a backend that uses timestamps, so
   we'll need to add another constructor to <codeph id="131-x-13">Key</codeph>. This could go on for a while.
   Fortunately, we already have a sum type intended for representing arbitrary data:
    <codeph id="131-x-14">PersistValue</codeph>:</p>
  <codeblock outputclass="haskell" id="131-x8">newtype Key entity = Key PersistValue</codeblock>
  <p id="131-x9">But this has another problem. Let's say we have a web application that takes an ID as a
   parameter from the user. It will need to receive that parameter as <codeph id="131-x-15">Text</codeph> and then
   try to convert it to a <codeph id="131-x-16">Key</codeph>. Well, that's simple: write a function to convert a
    <codeph id="131-x-17">Text</codeph> to a <codeph id="131-x-18">PersistValue</codeph>, and then wrap the result in the
    <codeph id="131-x-19">Key</codeph> constructor, right?</p>
  <p id="131-x10">Wrong. We tried this, and there's a big problem. We end up getting
    <codeph id="131-x-20">Key</codeph>s that could never be. For example, if we're dealing with SQL, a key must be
   an integer. But the approach described above would allow arbitrary textual data in. The result
   was a bunch of 500 server errors as the database choked on comparing an integer column to
   text.</p>
  <p id="131-x11">So what we need is a way to convert text to a <codeph id="131-x-21">Key</codeph>, but have it
   dependent on the rules of the backend in question. And once phrased that way, the answer is
   simple: just add another phantom. The real, actual definition of <codeph id="131-x-22">Key</codeph> in
   Persistent is:</p>
  <codeblock id="131-x12" outputclass="haskell">newtype Key backend entity = Key { unKey :: PersistValue }</codeblock>
  <p id="131-x13">This works great: we can have a <codeph id="131-x14">Text -&gt; Key MongoDB entity</codeph> function and a
    <codeph id="131-x15">Text -&gt; Key SqlPersist entity</codeph> function, and everything runs smoothly. But now
   we have a new problem: relations. Let's say we want to represent blogs and blog posts. We would
   use the entity definition:</p>
  <codeblock outputclass="entities" id="131-x16">Blog
    title Text
Post
    title Text
    blogId BlogId</codeblock>
  <p id="131-x17">But what would that look like in terms of our <codeph id="131-x-23">Key</codeph> datatype?</p>
  <codeblock id="131-x18" outputclass="haskell">data Blog = Blog { blogTitle :: Text }
data Post = Post { postTitle :: Text, postBlogId :: Key &lt;what goes here?&gt; Blog }</codeblock>
  <p id="131-x19">We need something to fill in as the backend. In theory, we could hardcode this to
    <codeph id="131-x-24">SqlPersist</codeph>, or <codeph id="131-x-25">Mongo</codeph>, but then our datatypes will only work
   for a single backend. For an individual application, that might be acceptable, but what about
   libraries defining datatypes to be used by multiple applications, using multiple backends?</p>
  <p id="131-x20">So things got a little more complicated. Our types are actually:</p>
  <codeblock id="131-x21" outputclass="haskell">data BlogGeneric backend = Blog { blogTitle :: Text }
data PostGeneric backend = Post { postTitle :: Text, postBlogId :: Key backend (BlogGeneric backend) }</codeblock>
  <p id="131-x22">Notice that we still keep the short names for the constructors and the records. Finally, to
   give a simple interface for normal code, we define some type synonyms:</p>
  <codeblock outputclass="haskell" id="131-x23">type Blog = BlogGeneric SqlPersist
type BlogId = Key SqlPersist Blog
type Post = PostGeneric SqlPersist
type PostId = Key SqlPersist Post</codeblock>
  <p id="131-x24">And no, <codeph id="131-x-26">SqlPersist</codeph> isn't hard-coded into Persistent anywhere. That
    <codeph id="131-x-27">sqlSettings</codeph> parameter you've been passing to <codeph id="131-x-28">mkPersist</codeph> is what
   tells us to use <codeph id="131-x-29">SqlPersist</codeph>. Mongo code will use <codeph id="131-x-30">mongoSettings</codeph>
   instead.</p>
  <p id="131-x25">This might be quite complicated under the surface, but user code hardly ever touches
   this. Look back through this whole chapter: not once did we need to deal with the
    <codeph id="131-x-31">Key</codeph> or <codeph id="131-x-32">Generic</codeph> stuff directly. The most common place for it to
   pop up is in compiler error messages. So it's important to be aware that this exists, but it
   shouldn't affect you on a day-to-day basis.</p>
 </conbody></concept><concept id="custom-fields-132"><title>Custom Fields</title><conbody><p id="132-cf1">Occassionally, you will want to define a custom field to be used in your datastore. The most common case is an enumeration, such as employment status. For this, Persistent provides a helper Template Haskell function:</p><codeblock id="132-x1" outputclass="haskell">-- START
{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, TemplateHaskell,
             OverloadedStrings, GADTs, FlexibleContexts #-}
import Database.Persist
import Database.Persist.Sqlite
import Database.Persist.TH

data Employment = Employed | Unemployed | Retired
    deriving (Show, Read, Eq)
derivePersistField "Employment"

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    name String
    employment Employment
|]

main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll

    insert $ Person "Bruce Wayne" Retired
    insert $ Person "Peter Parker" Unemployed
    insert $ Person "Michael" Employed
-- STOP</codeblock><p id="132-cf2"><codeph id="132-x-3">derivePersistField</codeph> stores the data in the database using a string
            field, and performs marshaling using the <codeph id="132-x-4">Show</codeph> and <codeph id="132-x-5">Read</codeph>
            instances of the datatype. This may not be as efficient as storing via an integer, but
            it is much more future proof: even if you add extra constructors in the future, your
            data will still be valid.</p></conbody></concept><concept id="persistent-raw-sql-133"><title>Persistent: Raw SQL</title><conbody>  <p id="133-x1">The Persistent package provides a type safe interface to data stores. It tries to be
            backend-agnostic, such as not relying on relational features of SQL. My experience has
            been you can easily perform 95% of what you need to do with the high-level interface.
            (In fact, most of my web apps use the high level interface exclusively.)</p>
  <p id="133-x2">But occassionally you'll want to use a feature that's specific to a backend. One feature I've
   used in the past is full text search. In this case, we'll use the SQL "LIKE" operator, which is
   not modeled in Persistent. We'll get all people with the last name "Snoyman" and print the
   records out.</p>
        <note id="133-x4">Actually, you <i id="133-x5">can</i> express a LIKE operator directly in the
            normal syntax due to a feature added in Persistent 0.6, which allows backend-specific
            operators. But this is still a good example, so let's roll with it.</note>
  <codeblock id="133-x3" outputclass="haskell">{-# LANGUAGE OverloadedStrings, TemplateHaskell, QuasiQuotes, TypeFamilies #-}
{-# LANGUAGE GeneralizedNewtypeDeriving, GADTs, FlexibleContexts #-}
import Database.Persist.Sqlite (withSqliteConn)
import Database.Persist.TH (mkPersist, persist, share, mkMigrate, sqlSettings)
import Database.Persist.GenericSql (runSqlConn, runMigration, SqlPersist)
import Database.Persist.GenericSql.Raw (withStmt)
import Data.Text (Text)
import Database.Persist
import Database.Persist.Store (PersistValue)
import Control.Monad.IO.Class (liftIO)
import qualified Data.Conduit as C
import qualified Data.Conduit.List as CL

share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    name Text
|]

main :: IO ()
main = withSqliteConn ":memory:" $ runSqlConn $ do
    runMigration migrateAll
    insert $ Person "Michael Snoyman"
    insert $ Person "Miriam Snoyman"
    insert $ Person "Eliezer Snoyman"
    insert $ Person "Gavriella Snoyman"
    insert $ Person "Greg Weber"
    insert $ Person "Rick Richardson"

    -- Persistent does not provide the LIKE keyword, but we'd like to get the
    -- whole Snoyman family...
    let sql = "SELECT name FROM Person WHERE name LIKE '%Snoyman'"
    C.runResourceT $ withStmt sql []
                C.$$ CL.mapM_ $ liftIO . print</codeblock>
        <p id="133-x-3">There is also higher-level support that allows for automated data marshaling. Please see
            the Haddock API docs for more details.</p>
 </conbody></concept><concept id="integration-with-yesod-134"><title>Integration with Yesod</title><conbody>
  <p id="134-x1">So you've been convinced of the power of Persistent. How do you integrate it with your
            Yesod application? If you use the scaffolding, most of the work is done for you already.
            But as we normally do, we'll build up everything manually here to point out how it works
            under the surface.</p>
  <p id="134-x2">The <apiname id="134-x3">yesod-persistent</apiname> package provides the meeting point
            between Persistent and Yesod. It provides the <codeph id="134-x-3">YesodPersist</codeph>
            typeclass, which standardizes access to the database via the <codeph id="134-x-4">runDB</codeph> method. Let's see this in action.</p>
  <codeblock id="134-x4" outputclass="haskell">{-# LANGUAGE QuasiQuotes, TypeFamilies, GeneralizedNewtypeDeriving, FlexibleContexts #-}
{-# LANGUAGE TemplateHaskell, OverloadedStrings, GADTs, MultiParamTypeClasses #-}
import Yesod
import Database.Persist.Sqlite

-- Define our entities as usual
share [mkPersist sqlSettings, mkMigrate "migrateAll"] [persist|
Person
    firstName String
    lastName String
    age Int Gt Desc
    deriving Show
|]

-- We keep our connection pool in the foundation. At program initialization, we
-- create our initial pool, and each time we need to perform an action we check
-- out a single connection from the pool.
data PersistTest = PersistTest ConnectionPool

-- We'll create a single route, to access a person. It's a very common
-- occurrence to use an Id type in routes.
mkYesod "PersistTest" [parseRoutes|
/person/#PersonId PersonR GET
|]

-- Nothing special here
instance Yesod PersistTest

-- Now we need to define a YesodPersist instance, which will keep track of
-- which backend we're using and how to run an action.
instance YesodPersist PersistTest where
    type YesodPersistBackend PersistTest = SqlPersist

    runDB action = do
        PersistTest pool &lt;- getYesod
        runSqlPool action pool

-- We'll just return the show value of a person, or a 404 if the Person doesn't
-- exist.
getPersonR :: PersonId -&gt; Handler RepPlain
getPersonR personId = do
    person &lt;- runDB $ get404 personId
    return $ RepPlain $ toContent $ show person

openConnectionCount :: Int
openConnectionCount = 10

main :: IO ()
main = withSqlitePool "test.db3" openConnectionCount $ \pool -&gt; do
    runSqlPool (runMigration migrateAll) pool
    runSqlPool (insert $ Person "Michael" "Snoyman" 26) pool
    warpDebug 3000 $ PersistTest pool</codeblock>
  <p id="134-x5">There are two important pieces here for general use. <codeph id="134-x-5">runDB</codeph> is used to
            run a DB action from within a <codeph id="134-x-6">Handler</codeph>. Within the
                <codeph id="134-x-7">runDB</codeph>, you can use any of the functions we've spoken about so far,
            such as <codeph id="134-x-8">insert</codeph> and <codeph id="134-x-9">selectList</codeph>.</p>
        <note>The type of <codeph>runDB</codeph> is <codeph>runDB :: YesodDB sub master a -&gt; GHandler
                sub master a</codeph>. <codeph>YesodDB</codeph> is defined
            as:<codeblock outputclass="haskell">type YesodDB sub master = YesodPersistBackend master (GHandler sub master)</codeblock>Since
            it is built on top of the <codeph>YesodPersistBackend</codeph> associated type, it uses
            the appropriate database backend based on the current site.</note>
  <p id="134-x6">The other new feature is <codeph id="134-x-10">get404</codeph>. It works just like
                <codeph id="134-x-11">get</codeph>, but instead of returning a <codeph id="134-x-12">Nothing</codeph> when a
            result can't be found, it returns a 404 message page. The <codeph id="134-x-13">getPersonR</codeph>
            function is a very common approach used in real-world Yesod applications:
                <codeph id="134-x-14">get404</codeph> a value and then return a response based on it.</p>
 </conbody></concept><concept id="summary-135"><title>Summary</title><conbody>
  <p id="135-x-3">Persistent brings the type safety of Haskell to your data access layer. Instead of writing
   error-prone, untyped data access, or manually writing boilerplate marshal code, you can rely on
   Persistent to automate the process for you.</p>
  <p id="135-x-4">The goal is to provide everything you need, <i id="135-x-5">most</i> of the time. For the times when you
   need something a bit more powerful, Persistent gives you direct access to the underlying data
   store, so you can write whatever 5-way joins you want.</p>
  <p id="135-x-6">Persistent integrates directly into the general Yesod workflow. Not only do helper packages
   like <codeph id="135-x-7">yesod-persistent</codeph> provide a nice layer, but packages like
    <codeph id="135-x-8">yesod-form</codeph> and <codeph id="135-x-9">yesod-auth</codeph> also leverage Persistent's features
   as well.</p>
 </conbody></concept></concept>