<?xml version="1.0" encoding="UTF-8"?><!DOCTYPE chapter PUBLIC "-//OASIS//DTD DocBook XML 5.0//EN" "http://www.oasis-open.org/docbook/xml/5.0/docbook.dtd"><chapter>
   <title>Conduit</title>
   <para/>
   <para/>
   <para>Conduits are a solution to the streaming data problem. Often times, laziness allows us
   to process large amounts of data without pulling all values into memory. However, doing so in the
   presence of I/O requires us to use <glossterm>lazy I/O</glossterm>. The main downside to lazy I/O is
   non-determinism: we have no guarantees of when our resource finalizers will be run. For small
   application, this may be acceptable, but for a high-load server, we could quickly run out of
   scarce resources, such as file handles.</para>
   <para>Conduits allow us to process large streams of data while still retaining deterministic resource
   handling. They provide a unified interface for data streams, whether they come from files,
   sockets, or memory. And when combined with <literal>ResourceT</literal>, we can safely allocate
   resources, knowing that they will always be reclaimed- even in the presence of exceptions.</para>
   <para>This appendix covers version 0.2 of the <function>conduit</function> package.</para>
   <section>
    <title>Conduits in Five Minutes</title>
    <para/>
    <para>While a good understanding of the lower-level mechanics of conduits is advisable, you can get
   very far without it. Let's start off with some high-level examples. Don't worry if some of the
   details seem a bit magical right now. We'll cover everything in the course of this appendix.
   Let's start with the terminology, and then some sample code.</para>
    <variablelist>
     <varlistentry>
      <term>Source</term>
      <listitem>
       <para>A producer of data. The data could be in a file, coming from a socket, or in memory as a
     list. To access this data, we <glossterm>pull</glossterm> from the source.</para>
      </listitem>
     </varlistentry>
     <varlistentry>
      <term>Sink</term>
      <listitem>
       <para>A consumer of data. Basic examples would be a sum function (adding up a stream of numbers
     fed in), a file sink (which writes all incoming bytes to a file), or a socket. We
      <glossterm>push</glossterm> data into a sink. When the sink finishes processing (we'll explain that
     later), it returns some value.</para>
      </listitem>
     </varlistentry>
     <varlistentry>
      <term>Conduit</term>
      <listitem>
       <para>A transformer of data. The simplest example is a map function, though there are many others.
     Like a sink, we <glossterm>push</glossterm> data into a conduit. But instead of returning a single value
     at the end, a conduit can return multiple outputs every time it is pushed to.</para>
      </listitem>
     </varlistentry>
     <varlistentry>
      <term>Fuse</term>
      <listitem>
       <para>(Thanks to David Mazieres for the term.) A conduit can be <glossterm>fused</glossterm> with a source
     to produce a new, modified source (the <literal>$=</literal> operator). For example, you could
     have a source that reads bytes from a file, and a conduit that decodes bytes into text. If you
     fuse them together, you would now have a source that reads text from a file. Likewise, a
     conduit and a sink can fuse into a new sink (<literal>=$</literal>), and two conduits can fuse
     into a new conduit (<literal>=$=</literal>).</para>
      </listitem>
     </varlistentry>
     <varlistentry>
      <term>Connect</term>
      <listitem>
       <para>You can connect a source to a sink using the <literal>$$</literal> operator. Doing so will
     pull data from the source and push it to the sink, until either the source or sink signals that
     they are "done."</para>
      </listitem>
     </varlistentry>
    </variablelist>
    <para>Let's see some examples of conduit code.</para>
    <programlisting language="haskell">{-# LANGUAGE OverloadedStrings #-}
import Data.Conduit -- the core library
import qualified Data.Conduit.List as CL -- some list-like functions
import qualified Data.Conduit.Binary as CB -- bytes
import qualified Data.Conduit.Text as CT

import Data.ByteString (ByteString)
import Data.Text (Text)
import qualified Data.Text as T
import Control.Monad.ST (runST)

-- Let's start with the basics: connecting a source to a sink. We'll use the
-- built in file functions to implementing efficient, constant-memory,
-- resource-friendly file copying.
--
-- Two things to note: we use $$ to connect our source to our sink, and then
-- use runResourceT.
copyFile :: FilePath -&gt; FilePath -&gt; IO ()
copyFile src dest = runResourceT $ CB.sourceFile src $$ CB.sinkFile dest


-- The Data.Conduit.List module provides a number of helper functions for
-- creating sources, sinks, and conduits. Let's look at a typical fold: summing
-- numbers.
sumSink :: Resource m =&gt; Sink Int m Int
sumSink = CL.fold (+) 0

-- If we want to go a little more low-level, we can code our sink with the
-- sinkState function. This function takes three parameters: an initial state,
-- a push function (receive some more data), and a close function.
sumSink2 :: Resource m =&gt; Sink Int m Int
sumSink2 = sinkState
    0 -- initial value

    -- update the state with the new input and
    -- indicate that we want more input
    (\accum i -&gt; return $ StateProcessing (accum + i))
    (\accum -&gt; return accum) -- return the current accum value on close

-- Another common helper function is sourceList. Let's see how we can combine
-- that function with our sumSink to reimplement the built-in sum function.
sum' :: [Int] -&gt; Int
sum' input = runST $ runResourceT $ CL.sourceList input $$ sumSink

-- Since this is Haskell, let's write a source to generate all of the
-- Fibonacci numbers. We'll use sourceState. The state will contain the next
-- two numbers in the sequence. We also need to provide a pull function, which
-- will return the next number and update the state.
fibs :: Resource m =&gt; Source m Int
fibs = sourceState
    (0, 1) -- initial state
    (\(x, y) -&gt; return $ StateOpen (y, x + y) x)

-- Suppose we want to get the sum of the first 10 Fibonacci numbers. We can use
-- the isolate conduit to make sure the sum sink only consumes 10 values.
sumTenFibs :: Int
sumTenFibs =
       runST -- runs fine in pure code
     $ runResourceT
     $ fibs
    $= CL.isolate 10 -- fuse the source and conduit into a source
    $$ sumSink

-- We can also fuse the conduit into the sink instead, we just swap a few
-- operators.
sumTenFibs2 :: Int
sumTenFibs2 =
       runST
     $ runResourceT
     $ fibs
    $$ CL.isolate 10
    =$ sumSink

-- Alright, let's make some conduits. Let's turn our numbers into text. Sounds
-- like a job for a map...

intToText :: Int -&gt; Text -- just a helper function
intToText = T.pack . show

textify :: Resource m =&gt; Conduit Int m Text
textify = CL.map intToText

-- Like previously, we can use a conduitState helper function. But here, we
-- don't even need state, so we provide a dummy state value.
textify2 :: Resource m =&gt; Conduit Int m Text
textify2 = conduitState
    ()
    (\() input -&gt; return $ StateProducing () [intToText input])
    (\() -&gt; return [])

-- Let's make the unlines conduit, that puts a newline on the end of each piece
-- of input. We'll just use CL.map; feel free to write it with conduitState as
-- well for practice.
unlines' :: Resource m =&gt; Conduit Text m Text
unlines' = CL.map $ \t -&gt; t `T.append` "\n"

-- And let's write a function that prints the first N fibs to a file. We'll
-- use UTF8 encoding.
writeFibs :: Int -&gt; FilePath -&gt; IO ()
writeFibs count dest =
      runResourceT
    $ fibs
   $= CL.isolate count
   $= textify
   $= unlines'
   $= CT.encode CT.utf8
   $$ CB.sinkFile dest

-- We used the $= operator to fuse the conduits into the sources, producing a
-- single source. We can also do the opposite: fuse the conduits into the sink. We can even combine the two.
writeFibs2 :: Int -&gt; FilePath -&gt; IO ()
writeFibs2 count dest =
      runResourceT
    $ fibs
   $= CL.isolate count
   $= textify
   $$ unlines'
   =$ CT.encode CT.utf8
   =$ CB.sinkFile dest

-- Or we could fuse all those inner conduits into a single conduit...
someIntLines :: ResourceThrow m -- encoding can throw an exception
             =&gt; Int
             -&gt; Conduit Int m ByteString
someIntLines count =
      CL.isolate count
  =$= textify
  =$= unlines'
  =$= CT.encode CT.utf8

-- and then use that conduit
writeFibs3 :: Int -&gt; FilePath -&gt; IO ()
writeFibs3 count dest =
      runResourceT
    $ fibs
   $= someIntLines count
   $$ CB.sinkFile dest

main :: IO ()
main = do
    putStrLn $ "First ten fibs: " ++ show sumTenFibs
    writeFibs 20 "fibs.txt"
    copyFile "fibs.txt" "fibs2.txt"</programlisting>
   </section>
   <section>
    <title>Structure of this Chapter</title>
    <para/>
    <para>The remainder of this chapter covers five major topics in conduits:</para>
    <itemizedlist>
     <listitem>
      <para>
       <literal>ResourceT</literal>, the underlying technique that allows us to have guaranteed
    resource deallocation.</para>
     </listitem>
     <listitem>
      <para>Sources, our data producers</para>
     </listitem>
     <listitem>
      <para>Sinks, our data consumers</para>
     </listitem>
     <listitem>
      <para>Conduits, our data transformers</para>
     </listitem>
     <listitem>
      <para>Buffering, which allows us to avoid Inversion of Control</para>
     </listitem>
    </itemizedlist>
   </section>
   <section>
    <title>The Resource monad transformer</title>
    <para/>
    <para>The Resource transformer (<literal>ResourceT</literal>) plays a vital role in proper resource
   management in the conduit project. It is included within the <literal>conduit</literal> package
   itself. We'll explaining <literal>ResourceT</literal> as its own entity. While some of the design
   decisions clearly are biased towards conduits, <literal>ResourceT</literal> should remain a usable
   tool in its own right.</para>
    <section>
     <title>Goals</title>
     <para/>
     <para>What's wrong with the following code?</para>
     <programlisting language="haskell">import System.IO

main = do
    output &lt;- openFile "output.txt" WriteMode
    input  &lt;- openFile "input.txt"  ReadMode
    hGetContents input &gt;&gt;= hPutStr output
    hClose input
    hClose output</programlisting>
     <para>If the file <filename>input.txt</filename> does not exist, then an exception will be thrown
   when trying to open it. As a result, <literal>hClose output</literal> will never be called, and
   we'll have leaked a scarce resource (a file descriptor). In our tiny program, this isn't a big
   deal, but clearly we can't afford such waste in a long running, highly active server process.</para>
     <para>Fortunately, solving the problem is easy:</para>
     <programlisting language="haskell">import System.IO

main =
    withFile "output.txt" WriteMode $ \output -&gt;
    withFile "input.txt" ReadMode $ \input -&gt;
    hGetContents input &gt;&gt;= hPutStr output</programlisting>
     <para>
      <literal>withFile</literal> makes sure that the <literal>Handle</literal> is always closed, even in
   the presence of exceptions. It also handles asynchronous exceptions. Overall, it's a great
   approach to use... when you can use it. While often <literal>withFile</literal> is easy to use,
   sometimes it can require restructuring our programs. And this restructuring can range from mildly
   tedious to wildly inefficient.</para>
     <para>Let's take enumerators for example. If you look in the documentation, there is an
    <literal>enumFile</literal> function (for reading contents from a file), but no
    <literal>iterFile</literal> (for writing contents to a file). That's because the flow of control
   in an iteratee doesn't allow proper allocation of the Handle. Instead, in order to write to a
   file, you need to allocate the Handle before entering the Iteratee, e.g.:</para>
     <programlisting language="haskell">import System.IO
import Data.Enumerator
import Data.Enumerator.Binary

main =
    withFile "output.txt" WriteMode $ \output -&gt;
    run_ $ enumFile "input.txt" $$ iterHandle output</programlisting>
     <para>This code works fine, but imagine that, instead of simply piping data directly to the file,
   there was a huge amount of computation that occurred before we need to use the output handle. We
   will have allocated a file descriptor long before we needed it, and thereby locked up a scarce
   resource in our application. Besides this, there are times when we <emphasis>can't</emphasis> allocate the file
   before hand, such as when we won't know which file to open until we've read from the input
   file.</para>
     <para>One of the stated goals of conduits is to solve this problem, and it does so via
    <literal>ResourceT</literal>. As a result, the above program can be written in conduit as:</para>
     <programlisting language="haskell">{-# LANGUAGE OverloadedStrings #-}
import Data.Conduit
import Data.Conduit.Binary

main = runResourceT $ sourceFile "input.txt" $$ sinkFile "output.txt"</programlisting>
    </section>
    <section>
     <title>How it Works</title>
     <para/>
     <para>There are essentially three base functions on <literal>ResourceT</literal>, and then a bunch of
   conveniences thrown on top. The first function is:</para>
     <programlisting language="haskell">register :: IO () -&gt; ResourceT IO ReleaseKey</programlisting>
     <note>
      <para>This function, and the others below, are actually more polymorphic than implied here,
   allowing other monads besides <literal>IO</literal>. In fact, almost any transformer on top of
    <literal>IO</literal>, as well as any <literal>ST</literal> stacks, work. We'll cover the details of
   that later.</para>
     </note>
     <para>This function registers a piece of code that it asserts <emphasis role="bold">must</emphasis> be run. It gives back a
    <literal>ReleaseKey</literal>, which is used by the next function:</para>
     <programlisting language="haskell">release :: ReleaseKey -&gt; ResourceT IO ()</programlisting>
     <para>Calling <literal>release</literal> on a <literal>ReleaseKey</literal> immediately performs the
   action you previously registered. You may call <literal>release</literal> on the same
    <literal>ReleaseKey</literal> as many times as you like; the first time it is called, it
    <glossterm>unregisters</glossterm> the action. This means you can safely register an action like a memory
   free, and have no concerns that it will be called twice.</para>
     <para>Eventually, we'll want to exit our special <literal>ResourceT</literal>. To do so, we use:</para>
     <programlisting language="haskell">runResourceT :: ResourceT IO a -&gt; IO a</programlisting>
     <para>This seemingly innocuous function is where all the magic happens. It runs through all of the
   registered cleanup actions and performs them. It is fully exception safe, meaning the cleanups
   will be performed in the presence of both synchronous and asynchronous exceptions. And as
   mentioned before, calling <literal>release</literal> will unregister an action, so there is no
   concern of double-freeing.</para>
     <para>Finally, as a convenience, we provide one more function for the common case of allocating a
   resource and registering a release action:</para>
     <programlisting language="haskell">with :: IO a -- ^ allocate
     -&gt; (a -&gt; IO ()) -- ^ free resource
     -&gt; ResourceT IO (ReleaseKey, a)</programlisting>
     <para>So, to rework our first buggy example to use <literal>ResourceT</literal>, we would write:</para>
     <programlisting language="haskell">import System.IO
import Control.Monad.Trans.Resource
import Control.Monad.Trans.Class (lift)

main = runResourceT $ do
    (releaseO, output) &lt;- with (openFile "output.txt" WriteMode) hClose
    (releaseI, input)  &lt;- with (openFile "input.txt"  ReadMode)  hClose
    lift $ hGetContents input &gt;&gt;= hPutStr output
    release releaseI
    release releaseO</programlisting>
     <para>Now there is no concern of any exceptions preventing the releasing of resources. We could skip
   the <literal>release</literal> calls if we want to, and in an example this small, it would not make
   any difference. But for larger applications, where we want processing to continue, this ensures
   that the <literal>Handle</literal>s are freed as early as possible, keeping our scarce resource
   usage to a minimum.</para>
    </section>
    <section>
     <title>Some Type Magic</title>
     <para/>
     <para>As alluded to, there's a bit more to <literal>ResourceT</literal> than simply running in
    <literal>IO</literal>. Let's cover some of the things we need from this underlying
    <literal>Monad</literal>.</para>
     <itemizedlist>
      <listitem>
       <para>Mutable references to keep track of the registered release actions. You might think we could
    just use a <literal>StateT</literal> transformer, but then our state wouldn't survive
    exceptions.</para>
      </listitem>
      <listitem>
       <para>We only want to register actions in the <glossterm>base monad</glossterm>. For example, if we have a
     <literal>ResourceT (WriterT [Int] IO)</literal> stack, we only want to register
     <literal>IO</literal> actions. This makes it easy to lift our stacks around (i.e., add an extra
    transformer to the middle of an existing stack), and avoids confusing issues about the threading
    of other monadic side-effects.</para>
      </listitem>
      <listitem>
       <para>Some way to guarantee an action is performed, even in the presence of exceptions. This boils
    down to needing a <literal>bracket</literal>-like function.</para>
      </listitem>
     </itemizedlist>
     <para>For the first point, we define a new typeclass to represent monads that have mutable
   references:</para>
     <programlisting language="haskell">class Monad m =&gt; HasRef m where
    type Ref m :: * -&gt; *
    newRef' :: a -&gt; m (Ref m a)
    readRef' :: Ref m a -&gt; m a
    writeRef' :: Ref m a -&gt; a -&gt; m ()
    modifyRef' :: Ref m a -&gt; (a -&gt; (a, b)) -&gt; m b
    mask :: ((forall a. m a -&gt; m a) -&gt; m b) -&gt; m b
    mask_ :: m a -&gt; m a
    try :: m a -&gt; m (Either SomeException a)</programlisting>
     <para>We have an associated type to signify what the reference type should be. (For fans of fundeps,
   you'll see in the next section that this <emphasis>has</emphasis> to be an associated type.) Then we provide a
   number of basic reference operations. Finally, there are some functions to help with exceptions,
   which are needed to safely implement the functions described in the last section. The instance
   for <literal>IO</literal> is very straight-forward:</para>
     <programlisting language="haskell">instance HasRef IO where
    type Ref IO = I.IORef
    newRef' = I.newIORef
    modifyRef' = I.atomicModifyIORef
    readRef' = I.readIORef
    writeRef' = I.writeIORef
    mask = E.mask
    mask_ = E.mask_
    try = E.try</programlisting>
     <para>However, we have a problem when it comes to implementing the <literal>ST</literal> instance:
   there is no way to deal with exceptions in the <literal>ST</literal> monad. As a result,
    <literal>mask</literal>, <literal>mask_</literal> and <literal>try</literal> are given default
   implementations that do no exception checking. This gives rise to the first word of warning:
    <emphasis role="bold">operations in the ST monad are not exception safe</emphasis>. You should not be allocating scarce
   resources in ST when using <literal>ResourceT</literal>. You might be wondering why bother with
    <literal>ResourceT</literal> at all then for <literal>ST</literal>. The answer is that there is a
   lot you can do with conduits without allocating scarce resources, and <literal>ST</literal> is a
   great way to do this in a pure way. But more on this later.</para>
     <para>Now onto point 2: we need some way to deal with this base monad concept. Again, we use an
   associated type (again explained in the next section). Our solution looks something like:</para>
     <programlisting language="haskell">class (HasRef (Base m), Monad m) =&gt; Resource m where
    type Base m :: * -&gt; *

    resourceLiftBase :: Base m a -&gt; m a</programlisting>
     <para>But we forgot about point 3: some <literal>bracket</literal>-like function. So we need one more
   method in this typeclass:</para>
     <programlisting language="haskell">    resourceBracket_ :: Base m a -&gt; Base m b -&gt; m c -&gt; m c</programlisting>
     <para>The reason the first two arguments to <literal>resourceBracket_</literal> (allocation and
   cleanup) live in <literal>Base m</literal> instead of <literal>m</literal> is that, in
    <literal>ResourceT</literal>, all allocation and cleanup lives in the base monad.</para>
     <para>So on top of our <literal>HasRef</literal> instance for <literal>IO</literal>, we now need a
    <literal>Resource</literal> instance as well. This is similarly straight-forward:</para>
     <programlisting language="haskell">instance Resource IO where
    type Base IO = IO
    resourceLiftBase = id
    resourceBracket_ = E.bracket_</programlisting>
     <para>We have similar <literal>ST</literal> instances, with <literal>resourceBracket_</literal> having no
   exception safety. The final step is dealing with monad transformers. We don't need to provide a
    <literal>HasRef</literal> instance, but we do need a <literal>Resource</literal> instance. The
   tricky part is providing a valid implementation of <literal>resourceBracket_</literal>. For this,
   we use some functions from <function>monad-control</function>:</para>
     <programlisting language="haskell">instance (MonadTransControl t, Resource m, Monad (t m))
        =&gt; Resource (t m) where
    type Base (t m) = Base m

    resourceLiftBase = lift . resourceLiftBase
    resourceBracket_ a b c =
        control' $ \run -&gt; resourceBracket_ a b (run c)
      where
        control' f = liftWith f &gt;&gt;= restoreT . return</programlisting>
     <para>For any transformer, its base is the base of its inner monad. Similarly, we lift to the base by
   lifting to the inner monad and then lifting to the base from there. The tricky part is the
   implemetnation of <literal>resourceBracket_</literal>. I will not go into a detailed explanation,
   as I would simply make a fool of myself.</para>
    </section>
    <section>
     <title>Definition of ResourceT</title>
     <para/>
     <para>We now have enough information to understand the definition of <literal>ResourceT</literal>:</para>
     <programlisting language="haskell">newtype ReleaseKey = ReleaseKey Int

type RefCount = Int
type NextKey = Int

data ReleaseMap base =
    ReleaseMap !NextKey !RefCount !(IntMap (base ()))

newtype ResourceT m a =
    ResourceT (Ref (Base m) (ReleaseMap (Base m)) -&gt; m a)</programlisting>
     <para>We see that <literal>ReleaseKey</literal> is simply an <literal>Int</literal>. If you skip a few
   lines down, this will make sense, since we're using an <literal>IntMap</literal> to keep track of
   the registered actions. We also define two type synonyms: <literal>RefCount</literal> and
    <literal>NextKey</literal>. <literal>NextKey</literal> keeps track of the most recently assigned
   value for a key, and is incremented each time <literal>register</literal> is called. We'll touch on
    <literal>RefCount</literal> later.</para>
     <para>The <literal>ReleaseMap</literal> is three pieces of information: the next key and the reference
   count, and then the map of all registered actions. Notice that <literal>ReleaseMap</literal> takes
   a type parameter <literal>base</literal>, which states which monad release actions must live
   in.</para>
     <para>Finally, a <literal>ResourceT</literal> is essentially a <literal>ReaderT</literal> that keeps a
   mutable reference to a <literal>ReleaseMap</literal>. The reference type is determined by the base
   of the monad in question, as is the cleanup monad. This is why we need to use associated
   types.</para>
     <para>The majority of the rest of the code in the <literal>Control.Monad.Trans.Resource</literal>
   module is just providing instances for the <literal>ResourceT</literal> type.</para>
    </section>
    <section>
     <title>Other Typeclasses</title>
     <para/>
     <para>There are three other typeclasses provided by the module:</para>
     <variablelist>
      <varlistentry>
       <term>ResourceUnsafeIO</term>
       <listitem>
        <para>Any monad which can lift <literal>IO</literal> actions into it, but that this may be
     considered unsafe. The prime candidate here is <literal>ST</literal>. Care should be taken to
     only lift actions which do not acquire scarce resources and which don't "fire the missiles." In
     other words, all the normal warnings of <literal>unsafeIOToST</literal> apply.</para>
       </listitem>
      </varlistentry>
      <varlistentry>
       <term>ResourceThrow</term>
       <listitem>
        <para>For actions that can throw exceptions. This automatically applies to all
     <literal>IO</literal>-based monads. For <literal>ST</literal>-based monads, you can use the
     supplied <literal>ExceptionT</literal> transformer to provide exception-throwing capabilities.
     Some functions in conduit, for example, will require this (e.g., text decoding).</para>
       </listitem>
      </varlistentry>
      <varlistentry>
       <term>ResourceIO</term>
       <listitem>
        <para>A convenience class tying together a bunch of other classes, included the two mentioned
     above. This is purely for convenience; you could achieve the same effect without this type
     class, you'd just have to do a lot more typing.</para>
       </listitem>
      </varlistentry>
     </variablelist>
    </section>
    <section>
     <title>Forking</title>
     <para/>
     <para>It would seem that forking a thread would be inherently unsafe with <literal>ResourceT</literal>,
   since the parent thread may call <literal>runResourceT</literal> while the child thread is still
   accessing some of the allocated resources. This is indeed true, <emphasis>if</emphasis> you use the normal
    <literal>forkIO</literal> function.</para>
     <note>
      <para>You can't actually use the standard <literal>forkIO</literal>, since it only operates in the
    <literal>IO</literal> monad, but you could use the <literal>fork</literal> function from
    <function>lifted-base</function>. In fact, due to this issue, the <function>regions</function>
   package does not provide a <literal>MonadBaseControl</literal> instance for its transformer (which
   is very similar to <literal>ResourceT</literal>). However, our goal in <literal>ResourceT</literal>
   is not to make it impossible for programmers to mess up, only to make it easier to do the right
   thing. Therefore, we still provide the instance, even though it could be abused.</para>
     </note>
     <para>In order to solve this, <literal>ResourceT</literal> includes reference counting. When you fork a
   new thread via <literal>resourceForkIO</literal>, the <literal>RefCount</literal> value of the
    <literal>ReleaseMap</literal> is incremented. Every time <literal>runResourceT</literal> is called,
   the value is decremented. Only when the value hits 0 are all the release actions called.</para>
    </section>
    <section>
     <title>Convenience Exports</title>
     <para/>
     <para>In addition to what's been listed so far, there are a few extra functions exported (mostly) for
   convenience.</para>
     <itemizedlist>
      <listitem>
       <para>
        <literal>newRef</literal>, <literal>writeRef</literal>, and <literal>readRef</literal> wrap up the
     <literal>HasRef</literal> versions of the functions and allow them to run in any
     <literal>ResourceT</literal>.</para>
      </listitem>
      <listitem>
       <para>
        <literal>withIO</literal> is essentially a type-restricted version of <literal>with</literal>,
    but working around some of the nastiness with types you would otherwise run into. In general:
    you'll want to use <literal>withIO</literal> when writing <literal>IO</literal> code.</para>
      </listitem>
      <listitem>
       <para>
        <literal>transResourceT</literal> let's you modify which monad your ResourceT is running in,
    assuming it keeps the same
    base.<programlisting language="haskell">transResourceT :: (Base m ~ Base n)
               =&gt; (m a -&gt; n a)
               -&gt; ResourceT m a
               -&gt; ResourceT n a
transResourceT f (ResourceT mx) = ResourceT (\r -&gt; f (mx r))</programlisting>
       </para>
      </listitem>
     </itemizedlist>
    </section>
   </section>
   <section>
    <title>Source</title>
    <para/>
    <para>I think it's simplest to understand sources by looking at the types:</para>
    <programlisting language="haskell">data SourceResult m a = Open (Source m a) a | Closed
data Source m a = Source
    { sourcePull :: ResourceT m (SourceResult m a)
    , sourceClose :: ResourceT m ()
    }</programlisting>
    <para>A source has just two operations on it: you can pull data from it, and you can close it (think
      of closing a file handle). When you pull, you either get some data and the a new
        <literal>Source</literal> (the source is still open), or nothing (the source is closed). Let's
      look at some of the simplest sources:</para>
    <programlisting language="haskell">import Prelude hiding (repeat)
import Data.Conduit

-- | Never give any data
eof :: Monad m =&gt; Source m a
eof = Source
    { sourcePull = return Closed
    , sourceClose = return ()
    }

-- | Always give the same value
repeat :: Monad m =&gt; a -&gt; Source m a
repeat a = Source
    { sourcePull = return $ Open (repeat a) a
    , sourceClose = return ()
    }
</programlisting>
    <para>These sources are very straight-forward, since they always return the same results.
      Additionally, their close records don't do anything. You might think that this is a bug:
      shouldn't a call to <literal>sourcePull</literal> return <literal>Closed</literal> after it's been
      closed? This isn't required, since one of the rules of sources is that they can never be
      reused. In other words:</para>
    <itemizedlist>
     <listitem>
      <para>If a <literal>Source</literal> returns <literal>Open</literal>, it has provided you with a new
          <literal>Source</literal> which you should use in place of the original one.</para>
     </listitem>
     <listitem>
      <para>If it returns <literal>Closed</literal>, then you cannot perform any more operations on
        it.</para>
     </listitem>
    </itemizedlist>
    <para>Don't worry too much about the invariant. In practice, you will almost never call
        <literal>sourcePull</literal> or <literal>sourceClose</literal> yourself. In fact, you hardly
      even write them yourself either (that's what <literal>sourceState</literal> and
        <literal>sourceIO</literal> are for). The point is that we can make some assumptions when we
      implement our sources.</para>
    <section>
     <title>State</title>
     <para/>
     <para>There is something similar about the two sources mentioned above: they never change. They
    <emphasis>always</emphasis> return the same value. In other words, they have no state. For almost all serious
   sources, we'll need some kind of state.</para>
     <note>
      <para>The state might actually be defined outside of our program. For example, if we write a
   source that reads data from a <literal>Handle</literal>, we don't need to manually specify any
   state, since the <literal>Handle</literal> itself already has.</para>
     </note>
     <para>The way we store state in a source is by updating the returned <literal>Source</literal> value in
   the <literal>Open</literal> constructor. This is best seen with an example.</para>
     <programlisting language="haskell">import Data.Conduit
import Control.Monad.Trans.Resource

-- | Provide data from the list, one element at a time.
sourceList :: Resource m =&gt; [a] -&gt; Source m a
sourceList list = Source
    { sourcePull =
        case list of
            [] -&gt; return Closed -- no more data

            -- This is where we store our state: by return a new
            -- Source with the rest of the list
            x:xs -&gt; return $ Open (sourceList xs) x
        , sourceClose = return ()
        }
</programlisting>
     <para>Each time we pull from the source, it checks the input list. If the list is empty, pulling
   returns <literal>Closed</literal>, which makes sense. If the list is not empty, pulling returns
    <literal>Open</literal> with both the next value in the list, and a new <literal>Source</literal>
   value containing the rest of the input list.</para>
    </section>
    <section>
     <title>sourceState and sourceIO</title>
     <para/>
     <para>In addition to being able to manually create <literal>Source</literal>s, we also have a few
   convenience functions that allow us to create most sources in a more high-level fashion.
    <literal>sourceState</literal> let's you write code similar to how you would use the
    <literal>State</literal> monad. You provide an initial state, your pull function is provided with
   the current state, and it returns a new state and a return value. Let's use this to reimplement
   sourceList.</para>
     <programlisting language="haskell">import Data.Conduit
import Control.Monad.Trans.Resource

-- | Provide data from the list, one element at a time.
sourceList :: Resource m =&gt; [a] -&gt; Source m a
sourceList state0 = sourceState
    state0
    pull
  where
    pull [] = return StateClosed
    pull (x:xs) = return $ StateOpen xs x
</programlisting>
     <para>Notice the usage of the <literal>StateClosed</literal> and <literal>StateOpen</literal>
   constructors. These are very similar to <literal>Closed</literal> and <literal>Open</literal>, except
   that instead of specifying the next <literal>Source</literal> to be used, you provide the next
   state (here, the remainder of the list).</para>
     <para>The other common activity is to perform some I/O allocation (like opening a file), registering
   some cleanup action (closing that file), and having a function for pulling data from that
   resource. <literal>conduit</literal> comes built-in with a <literal>sourceFile</literal> function
   that gives a stream of <literal>ByteString</literal>s. Let's write a wildly inefficient alternative
   that returns a stream of characters.</para>
     <programlisting language="haskell">import Data.Conduit
import Control.Monad.Trans.Resource
import System.IO
import Control.Monad.IO.Class (liftIO)

sourceFile :: ResourceIO m =&gt; FilePath -&gt; Source m Char
sourceFile fp = sourceIO
    (openFile fp ReadMode)
    hClose
    (\h -&gt; liftIO $ do
        eof &lt;- hIsEOF h
        if eof
            then return IOClosed
            else fmap IOOpen $ hGetChar h)
</programlisting>
     <para>Like <literal>sourceState</literal>, it uses a variant on the <literal>Open</literal> and
    <literal>Closed</literal> constructors. <literal>sourceIO</literal> does a number of things for
   us:</para>
     <itemizedlist>
      <listitem>
       <para>It registers the cleanup function with the <literal>ResourceT</literal> transformer, ensuring
    it gets called even in the presence of exceptions.</para>
      </listitem>
      <listitem>
       <para>It sets up the <literal>sourceClose</literal> record to release the resource immediately.</para>
      </listitem>
      <listitem>
       <para>As soon as you return <literal>IOClosed</literal>, it will release the resource.</para>
      </listitem>
     </itemizedlist>
    </section>
   </section>
   <section>
    <title>Sinks</title>
    <para/>
    <para>A sink consumes a stream of data, and produces a result. A sink must always produce a result,
   and must always produce a single result. This is encoded in the types themselves.</para>
    <para>There is a <literal>Monad</literal> instance for sink, making it simple to compose multiple sinks
   together into a larger sink. You can also use the built-in sink functions to perform most of your
   work. Like sources, you'll rarely need to dive into the inner workings. Let's start off with an
   example: getting lines from a stream of <literal>Char</literal>s (we'll assume Unix line endings
   for simplicity).</para>
    <programlisting language="haskell">import Data.Conduit
import qualified Data.Conduit.List as CL

-- Get a single line from the stream.
sinkLine :: Resource m =&gt; Sink Char m String
sinkLine = sinkState
    id -- initial state, nothing at the beginning of the line
    push
    close
  where
    -- On a new line, return the contents up until here
    push front '\n' =
        return $ StateDone Nothing $ front []

    -- Just another character, add it to the front and keep going
    push front char =
        return $ StateProcessing $ front . (char:)

    -- Got an EOF before hitting a newline, just give what we have so far
    close front = return $ front []

-- Get all the lines from the stream, until we hit a blank line or EOF.
sinkLines :: Resource m =&gt; Sink Char m [String]
sinkLines = do
    line &lt;- sinkLine
    if null line
        then return []
        else do
            lines &lt;- sinkLines
            return $ line : lines

content :: String
content = unlines
    [ "This is the first line."
    , "Here's the second."
    , ""
    , "After the blank."
    ]

main :: IO ()
main = do
    lines &lt;- runResourceT $ CL.sourceList content $$ sinkLines
    mapM_ putStrLn lines</programlisting>
    <para>Running this sample produces the expected output:</para>
    <programlisting>This is the first line.
Here's the second.</programlisting>
    <para>
     <literal>sinkLine</literal> demonstrates usage of the <literal>sinkState</literal> function, which
   is very similar to the <literal>sourceState</literal> function we just saw. It takes three
   arguments: an initial state, a push function (takes the current state and next input, and returns
   a new state and result) and a close function (takes the current state and returns an output). As
   opposed to <literal>sourceState</literal>- which doesn't need a close function- a sink is required
   to always return a result.</para>
    <para>Our push function has two clauses. When it gets a newline character, it indicates that
   processing is complete via <literal>StateDone</literal>. The <literal>Nothing</literal> indicates
   that there is no leftover input (we'll discuss that later). It also gives an output of all the
   characters it has received. The second clause simply appends the new character to the existing
   state and indicates that we are still working via <literal>StateProcessing</literal>. The close
   function returns all characters.</para>
    <para>
     <literal>sinkLines</literal> shows how we can use the monadic interface to produce new sinks. If
   you replace <literal>sinkLine</literal> with <literal>getLine</literal>, this would look like
   standard code to pull lines from standard input. This familiar interface should make it easy to
   get up and running quickly.</para>
    <section>
     <title>Types</title>
     <para/>
     <para>The types for sinks are just a bit more involved than sources. Let's have a look:</para>
     <programlisting language="haskell">type SinkPush input m output = input -&gt; ResourceT m (SinkResult input m output)
type SinkClose m output = ResourceT m output

data SinkResult input m output =
    Processing (SinkPush input m output) (SinkClose m output)
  | Done (Maybe input) output

data Sink input m output =
    SinkNoData output
  | SinkData
        { sinkPush :: SinkPush input m output
        , sinkClose :: SinkClose m output
        }
  | SinkLift (ResourceT m (Sink input m output))</programlisting>
     <para>Whenever a sink is pushed to, it can either say it needs more data
   (<literal>Processing</literal>) or say it's all done. When still processing, it must provided
   updated push and close function; when done, it returns any leftover inut and the output. Fairly
   straight-forward.</para>
     <para>The first real "gotcha" is the three constructors for <literal>Sink</literal>. Why do we need
    <literal>SinkNoData</literal>: aren't sinks all about consuming data? The answer is that we need
   it to efficiently implement our <literal>Monad</literal> instance. When we use
    <literal>return</literal>, we're giving back a value that requires no data in order to compute it.
   We could model this with the <literal>SinkData</literal> constructor, with something like:</para>
     <programlisting language="haskell">myReturn a = SinkData (\input -&gt; return (Done (Just input) a)) (return a)</programlisting>
     <para>But doing so would force reading in an extra bit of input that we don't need right now, and
   possibly will never need. (Have a look again at the <literal>sinkLines</literal> example.) So
   instead, we have an extra constructor to indicate that no input is required. Likewise,
    <literal>SinkLift</literal> is provided in order to implement an efficient
    <literal>MonadTrans</literal> instance.</para>
    </section>
    <section>
     <title>Sinks: no helpers</title>
     <para/>
     <para>Let's try to implement some sinks on the "bare metal", without any helper functions.</para>
     <programlisting language="haskell">import Data.Conduit
import System.IO
import Control.Monad.Trans.Resource
import Control.Monad.IO.Class (liftIO)

-- Consume all input and discard it.
sinkNull :: Resource m =&gt; Sink a m ()
sinkNull =
    SinkData push close
  where
    push _ignored = return $ Processing push close
    close = return ()

-- Let's stream characters to a file. Here we do need some kind of
-- initialization. We do this by initializing in a push function,
-- and then returning a different push function for subsequent
-- calls. By using withIO, we know that the handle will be closed even
-- if there's an exception.
sinkFile :: ResourceIO m =&gt; FilePath -&gt; Sink Char m ()
sinkFile fp =
    SinkData pushInit closeInit
  where
    pushInit char = do
        (releaseKey, handle) &lt;- withIO (openFile fp WriteMode) hClose
        push releaseKey handle char
    closeInit = do
        -- Never opened a file, so nothing to do here
        return ()

    push releaseKey handle char = do
        liftIO $ hPutChar handle char
        return $ Processing (push releaseKey handle) (close releaseKey handle)

    close releaseKey _ = do
        -- Close the file handle as soon as possible.
        return ()

-- And we'll count how many values were in the stream.
count :: Resource m =&gt; Sink a m Int
count =
    SinkData (push 0) (close 0)
  where
    push count _ignored =
        return $ Processing (push count') (close count')
      where
        count' = count + 1

    close count = return count
</programlisting>
     <para>Nothing is particularly complicated to implement. You should notice a common pattern here:
            declaring your push and close functions in a <literal>where</literal> clause, and then
            using them twice: once for the initial <literal>SinkData</literal>, and once for the
                <literal>Processing</literal> constructor. This can become a bit tedious; that's why
            we have helper functions.</para>
    </section>
    <section>
     <title>Sinks: with helpers</title>
     <para/>
     <para>Let's rewrite <literal>sinkFile</literal> and <literal>count</literal> to take advantage of the
   helper functions <literal>sinkIO</literal> and <literal>sinkState</literal>, respectively.</para>
     <programlisting language="haskell">import Data.Conduit
import System.IO
import Control.Monad.IO.Class (liftIO)

-- We never have to touch the release key directly, sinkIO automatically
-- releases our resource as soon as we return IODone from our push function,
-- or sinkClose is called.
sinkFile :: ResourceIO m =&gt; FilePath -&gt; Sink Char m ()
sinkFile fp = sinkIO
    (openFile fp WriteMode)
    hClose
    -- push: notice that we are given the handle and the input
    (\handle char -&gt; do
        liftIO $ hPutChar handle char
        return IOProcessing)
    -- close: we're also given the handle, but we don't use it
    (\_handle -&gt; return ())

-- And we'll count how many values were in the stream.
count :: Resource m =&gt; Sink a m Int
count = sinkState
    0
    -- The push function gets both the current state and the next input...
    (\state _ignored -&gt;
        -- and it returns the new state
        return $ StateProcessing $ state + 1)
    -- The close function gets the final state and returns the output.
    (\state -&gt; return state)
</programlisting>
     <para>Nothing dramatic, just slightly shorter, less error-prone code. Using these two helper
   functions is highly recommended, as it ensures proper resource management and state updating.</para>
    </section>
    <section>
     <title>List functions</title>
     <para/>
     <para>As easy as it is to write your own sinks, you'll likely want to take advantage of the built-in
   sinks available in the <function>Data.Conduit.List</function> module. These provide
   analogues to common list functions, like folding. (The module also has some
    <literal>Conduit</literal>s, like <function>map</function>.)</para>
     <para>If you're looking for some way to practice with conduits, reimplementing the functions in the
    <literal>List</literal> module- both with and without the helper functions- would be a good
   start.</para>
     <para>Let's look at some simple things we can make out of the built-in sinks.</para>
     <programlisting language="haskell">import Data.Conduit
import qualified Data.Conduit.List as CL
import Control.Monad.IO.Class (liftIO)

-- A sum function.
sum' :: Resource m =&gt; Sink Int m Int
sum' = CL.fold (+) 0

-- Print every input value to standard output.
printer :: (Show a, ResourceIO m) =&gt; Sink a m ()
printer = CL.mapM_ (liftIO . print)

-- Sum up all the values in a stream after the first five.
sumSkipFive :: Resource m =&gt; Sink Int m Int
sumSkipFive = do
    CL.drop 5
    CL.fold (+) 0

-- Print each input number and sum the total
printSum :: ResourceIO m =&gt; Sink Int m Int
printSum = do
    total &lt;- CL.foldM go 0
    liftIO $ putStrLn $ "Sum: " ++ show total
    return total
  where
    go accum int = do
        liftIO $ putStrLn $ "New input: " ++ show int
        return $ accum + int
</programlisting>
    </section>
    <section>
     <title>Connecting</title>
     <para/>
     <para>At the end of the day, we're actually going to want to use our sinks. While we could manually
   call <literal>sinkPush</literal> and <literal>sinkClose</literal>, it's tedious. For example:</para>
     <programlisting language="haskell">main :: IO ()
main = runResourceT $ do
    res &lt;-
        case printSum of
            SinkData push close -&gt; loop [1..10] push close
            SinkNoData res -&gt; return res
    liftIO $ putStrLn $ "Got a result: " ++ show res
  where
    loop [] _push close = close
    loop (x:xs) push close = do
        mres &lt;- push x
        case mres of
            Done _leftover res -&gt; return res
            Processing push' close' -&gt; loop xs push' close'
</programlisting>
     <para>Instead, the recommended approach is to connect your sink to a source. Not only is this
   simpler, it's less error prone, and means you have a lot of flexibility in where your data is
   coming from. To rewrite the example above:</para>
     <programlisting language="haskell">main :: IO ()
main = runResourceT $ do
    res &lt;- CL.sourceList [1..10] $$ printSum
    liftIO $ putStrLn $ "Got a result: " ++ show res
</programlisting>
     <para>Connecting takes care of testing for the sink constructor (<literal>SinkData</literal> versus
    <literal>SinkNoData</literal> versus <literal>SinkLift</literal>), pulling from the source, and
   pushing to/closing the sink.</para>
     <para>However, there is one thing I wanted to point out from the long-winded example. On the second
   to last line, we ignore the leftover value of <literal>Done</literal>. This brings up the issue of
    <glossterm>data loss</glossterm>. This is an important topic that has had a lot of thought put into it.
   Unfortunately, we can't fully cover it yet, as we haven't discussed the main culprit in the
   drama: <literal>Conduit</literal>s (the type, not the package).</para>
     <para>But as a quick note here, the leftover value from the <literal>Done</literal> constructor is not
   always ignored. The <literal>Monad</literal> instance, for example, uses it to pass data from one
   sink to the next in a binding. And in fact, the real connect operator <emphasis>doesn't</emphasis> always throw
   away the leftovers. When we cover resumable sources later, we'll see that the leftover value is
   put back on the buffer to allow later sinks reusing an existing source to pull the value.</para>
    </section>
   </section>
   <section>
    <title>Conduit</title>
    <para/>
    <para>This section covers the final major datatype in our package, conduits. While sources produce a
   stream of data and sinks consume a stream, conduits transform a stream.</para>
    <section>
     <title>Types</title>
     <para/>
     <para>As we did previously, let's start off by looking at the types involved.</para>
     <programlisting language="haskell">data ConduitResult input m output =
    Producing (Conduit input m output) [output]
  | Finished (Maybe input) [output]

data Conduit input m output = Conduit
    { conduitPush :: input -&gt; ResourceT m (ConduitResult input m output)
    , conduitClose :: ResourceT m [output]
    }</programlisting>
     <para>This should look very similar to what we've seen with sinks. A conduit can be pushed to, in
   which case it returns a result. A result either indicates that it is still producing data, or
   that it is finished. When a conduit is closed, it returns some more output.</para>
     <para>But let's examine the idiosyncracies a bit. Like sinks, we can only push one piece of input at
   a time, and leftover data may be 0 or 1 pieces. However, there are a few changes:</para>
     <itemizedlist>
      <listitem>
       <para>When producing (the equivalent of processing for a sink), we can return output. This is
    because a conduit will product a new stream of output instead of producing a single output value
    at the end of processing.</para>
      </listitem>
      <listitem>
       <para>A sink always returns a single output value, while a conduit returns 0 or more outputs (a
    list). To understand why, consider conduits such as <literal>concatMap</literal> (produces
    multiple outputs for one input) and <literal>filter</literal> (returns 0 or 1 output for each
    input).</para>
      </listitem>
      <listitem>
       <para>We have no special constructor like <literal>SinkNoData</literal>. That's because we provide no
     <literal>Monad</literal> instance for conduits. We'll see later how you can still use a familiar
    Monadic approach to creating conduits.</para>
      </listitem>
     </itemizedlist>
     <para>Overall conduits should seem very similar to what we've covered so far.</para>
    </section>
    <section>
     <title>Simple conduits</title>
     <para/>
     <para>We'll start off by defining some simple conduits that don't have any state.</para>
     <programlisting language="haskell">import Prelude hiding (map, concatMap)
import Data.Conduit

-- A simple conduit that just passes on the data as-is.
passThrough :: Monad m =&gt; Conduit input m input
passThrough = Conduit
    { conduitPush = \input -&gt; return $ Producing passThrough [input]
    , conduitClose = return []
    }

-- map values in a stream
map :: Monad m =&gt; (input -&gt; output) -&gt; Conduit input m output
map f = Conduit
    { conduitPush = \input -&gt; return $ Producing (map f) [f input]
    , conduitClose = return []
    }

-- map and concatenate
concatMap :: Monad m =&gt; (input -&gt; [output]) -&gt; Conduit input m output
concatMap f = Conduit
    { conduitPush = \input -&gt; return $ Producing (concatMap f) $ f input
    , conduitClose = return []
    }
</programlisting>
    </section>
    <section>
     <title>Stateful conduits</title>
     <para/>
     <para>Of course, not all conduits can be declared without state. Doing so on the bare metal is not
   too difficult.</para>
     <programlisting language="haskell">import Prelude hiding (reverse)
import qualified Data.List
import Data.Conduit
import Control.Monad.Trans.Resource

-- Reverse the elements in the stream. Note that this has the same downside as
-- the standard reverse function: you have to read the entire stream into
-- memory before producing any output.
reverse :: Resource m =&gt; Conduit input m input
reverse =
    mkConduit []
  where
    mkConduit state = Conduit (push state) (close state)
    push state input = return $ Producing (mkConduit $ input : state) []
    close state = return state

-- Same thing with sort: it will pull everything into memory
sort :: (Ord input, Resource m) =&gt; Conduit input m input
sort =
    mkConduit []
  where
    mkConduit state = Conduit (push state) (close state)
    push state input = return $ Producing (mkConduit $ input : state) []
    close state = return $ Data.List.sort state
</programlisting>
     <para>But we can do better. Just like <literal>sourceState</literal> and <literal>sinkState</literal>, we
            have <literal>conduitState</literal> to simplify things.</para>
     <programlisting language="haskell">import Prelude hiding (reverse)
import qualified Data.List
import Data.Conduit

-- Reverse the elements in the stream. Note that this has the same downside as
-- the standard reverse function: you have to read the entire stream into
-- memory before producing any output.
reverse :: Resource m =&gt; Conduit input m input
reverse =
    conduitState [] push close
  where
    push state input = return $ StateProducing (input : state) []
    close state = return state

-- Same thing with sort: it will pull everything into memory
sort :: (Ord input, Resource m) =&gt; Conduit input m input
sort =
    conduitState [] push close
  where
    push state input = return $ StateProducing (input : state) []
    close state = return $ Data.List.sort state
</programlisting>
    </section>
    <section>
     <title>Using conduits</title>
     <para/>
     <para>The way <literal>Conduit</literal>s interact with the rest of the package is via
    <glossterm>fusing</glossterm>. A conduit can be fused into a source, producing a new source, fused into a
   sink to produce a new sink, or fused with another conduit to produce a new conduit. It's best to
   just look at the fusion operators.</para>
     <programlisting language="haskell">-- Left fusion: source + conduit = source
($=) :: (Resource m, IsSource src) =&gt; src m a -&gt; Conduit a m b -&gt; Source m b

-- Right fusion: conduit + sink = sink
(=$) :: Resource m =&gt; Conduit a m b -&gt; Sink b m c -&gt; Sink a m c

-- Middle fusion: conduit + conduit = conduit
(=$=) :: Resource m =&gt; Conduit a m b -&gt; Conduit b m c -&gt; Conduit a m c</programlisting>
     <para>Using these operators is straightforward.</para>
     <programlisting language="haskell">useConduits = do
    runResourceT
          $  CL.sourceList [1..10]
          $= reverse
          $= CL.map show
          $$ CL.consume

    -- equivalent to
    runResourceT
          $  CL.sourceList [1..10]
          $$ reverse
          =$ CL.map show
          =$ CL.consume

    -- and equivalent to
    runResourceT
          $  CL.sourceList [1..10]
          $$ (reverse =$= CL.map show)
          =$ CL.consume</programlisting>
     <para>There is in fact one last way of expressing the same idea. I'll leave it as an exercise to the
   reader to discover it.</para>
     <para>It may seem like all these different approaches are redundant. While occassionally you can in
   fact choose whichever approach you feel like using, in many cases you will need a specific
   approach. For example:</para>
     <itemizedlist>
      <listitem>
       <para>If you have a stream of numbers, and you want to apply a conduit (e.g., <literal>map
     show</literal>) to only some of the stream that will be passed to a specific sink, you'll want
    to use the right fusion operator.</para>
      </listitem>
      <listitem>
       <para>If you're reading a file, and want to parse the entire file as textual data, you'll want to
    use left fusion to convert the entire stream.</para>
      </listitem>
      <listitem>
       <para>If you want to create reusable conduits that combine together individual, smaller conduits,
    you'll use middle fusion.</para>
      </listitem>
     </itemizedlist>
    </section>
    <section>
     <title>Data loss</title>
     <para/>
     <para>Let's forget about conduits for a moment. Instead, suppose we want to write a program- using
   plain old lists- that will take a list of numbers, apply some kind of transformation to them,
   take the first five transformed values and do something with them, and then do something else
   with the remaining <emphasis role="bold">non-transformed</emphasis> values. For example, we want something like:</para>
     <programlisting language="haskell">main = do
    let list = [1..10]
        transformed = map show list
        (begin, end) = splitAt 5 transformed
        untransformed = map read end
    mapM_ putStrLn begin
    print $ sum untransformed</programlisting>
     <para>But clearly this isn't a good general solution, since we don't want to have to transform and
   then untransform every element in the list. For one thing, we may not always have an inverse
   function. Another issue is efficiency. In this case, we can write something more efficient:</para>
     <programlisting language="haskell">main = do
    let list = [1..10]
        (begin, end) = splitAt 5 list
        transformed = map show begin
    mapM_ putStrLn transformed
    print $ sum end</programlisting>
     <para>Note the change: we perform our split before transforming any elements. This works because,
   with <literal>map</literal>, we have a 1-to-1 correspondence between the input and output elements.
   So splitting at 5 before or after mapping <literal>show</literal> is the same thing. But what
   happens if we replace <literal>map show</literal> with something more devious.</para>
     <programlisting language="haskell">deviousTransform =
    concatMap go
  where
    go 1 = [show 1]
    go 2 = [show 2, "two"]
    go 3 = replicate 5 "three"
    go x = [show x]</programlisting>
     <para>We no longer have the 1-to-1 correspondence. As a result, we can't use the second method. But
   it's even worse: we can't use the first method either, since there's no inverse of our
    <literal>deviousTransform</literal>.</para>
     <para>There's only one solution to the problem that I'm aware of: transform elements one at a time.
   The final program looks like this:</para>
     <programlisting language="haskell">deviousTransform 1 = [show 1]
deviousTransform 2 = [show 2, "two"]
deviousTransform 3 = replicate 5 "three"
deviousTransform x = [show x]

transform5 :: [Int] -&gt; ([String], [Int])
transform5 list =
    go [] list
  where
    go output (x:xs)
        | newLen &gt;= 5 = (take 5 output', xs)
        | otherwise = go output' xs
      where
        output' = output ++ deviousTransform x
        newLen = length output'

    -- Degenerate case: not enough input to make 5 outputs
    go output [] = (output, [])

main = do
    let list = [1..10]
        (begin, end) = transform5 list
    mapM_ putStrLn begin
    print $ sum end</programlisting>
     <para>The final output of this program is<programlisting>1
2
two
three
three
49</programlisting>What's important
   to note is that the number 3 is converted into five copies of the word "three", yet only two of
   them show up in the output. The rest are discarded in the <literal>take 5</literal> call.</para>
     <para>This whole exercise is just to demonstrate the issue of data loss in conduits. By forcing
   conduits to accept only one input at a time, we avoid the issue of transforming too many elements
   at once. That doesn't mean we don't lose <emphasis>any</emphasis> data: if a conduit produces too much output
   for the receiving sink to handle, some of it may be lost.</para>
     <para>To put all this another way: conduits avoid chunking to get away from data loss. This is not an
   issue unique to conduits. If you look in the implementation of <literal>concatMapM</literal> for
    <function>enumerator</function>, you'll see that it forces elements to be handled one at a time.
   In conduits, we opted to force the issue at the type level.</para>
    </section>
    <section>
     <title>SequencedSink</title>
     <para/>
     <para>Suppose we want to be able to combine up existing conduits and sinks to produce a new, more
   powerful conduit. For example, we want to write a conduit that takes a stream of numbers and sums
   up every five. In other words, for the input <literal>[1..50]</literal>, it should result in the
   sequence <literal>[15,40,65,90,115,140,165,190,215,240]</literal>. We can definitely do this with
   the low-level conduit interface.</para>
     <programlisting language="haskell">sum5Raw :: Resource m =&gt; Conduit Int m Int
sum5Raw =
    conduitState (0, 0) push close
  where
    push (total, count) input
        | newCount == 5 = return $ StateProducing (0, 0) [newTotal]
        | otherwise     = return $ StateProducing (newTotal, newCount) []
      where
        newTotal = total + input
        newCount = count + 1
    close (total, count)
        | count == 0 = return []
        | otherwise  = return [total]
</programlisting>
     <para>But this is frustrating, since we already have all the tools we need to do this at a high
   level! There's the <literal>fold</literal> sink for adding up the numbers, and the
    <literal>isolate</literal> conduit which will only allow up to a certain number of elements to be
   passed to a sink. Can't we combine these somehow?</para>
     <para>The answer is a <literal>SequencedSink</literal>. The idea is to create a normal
    <literal>Sink</literal>, except it returns a special output called a
    <literal>SequencedSinkResponse</literal>. This value can emit new output, stop processing data, or
   transfer control to a new conduit. (See the Haddocks for more information.) Then we can turn this
   into a <literal>Conduit</literal> using the <literal>sequenceSink</literal> function. This function
   also takes some state value that gets passed through to the sink.</para>
     <para>So we can rewrite <literal>sum5Raw</literal> in a much more high-level manner.</para>
     <programlisting language="haskell">sum5 :: Resource m =&gt; Conduit Int m Int
sum5 = sequenceSink () $ \() -&gt; do
    nextSum &lt;- CL.isolate 5 =$ CL.fold (+) 0
    return $ Emit () [nextSum]
</programlisting>
     <para>All of the <literal>()</literal> in there are simply the unused state variable being passed
   around, they can be ignored. Otherwise, we're doing exactly what we want. We fuse
    <literal>isolate</literal> to <literal>fold</literal> to get the sum of the next five elements from
   the stream. We then emit that value, and start all over again.</para>
     <para>Let's say we want to modify this slightly. We want to get the first 8 sums, and then pass
   through the remaining values, multiplied by 2. We can keep track of how many values we've
   returned in our state, and then use the <literal>StartConduit</literal> constructor to pass control
   to the multiply-by-2 conduit next.</para>
     <programlisting language="haskell">sum5Pass :: Resource m =&gt; Conduit Int m Int
sum5Pass = sequenceSink 0 $ \count -&gt; do
    if count == 8
        then return $ StartConduit $ CL.map (* 2)
        else do
            nextSum &lt;- CL.isolate 5 =$ CL.fold (+) 0
            return $ Emit (count + 1) [nextSum]
</programlisting>
     <para>These are obviously very contrived examples, but I hope it makes clear the power and simplicity
   available from this approach.</para>
    </section>
   </section>
   <section>
    <title>Buffering</title>
    <para/>
    <para>Buffering is one of the unique features of conduits. With buffering, conduits no longer need to
   control the flow of your application. In some cases, this can lead to simpler code.</para>
    <section>
     <title>Inversion of Control</title>
     <para/>
     <para>Buffering was actually one of the main motivations in the creation of the
    <literal>conduit</literal> package. To see its importance, we need to consider the approach we've
   seen so far, which we'll call inversion of control, or IoC.</para>
     <note>
      <para>Inversion of control can mean different things in different circles. If you object to its
   usage here, go ahead replace it with some other phrase like "warm, fuzzy thing." I won't be
   offended.</para>
     </note>
     <para>Suppose you want to count how many newline characters there are in a file. In the standard
   imperative approach, you would do someting like:</para>
     <orderedlist>
      <listitem>
       <para>Open the file</para>
      </listitem>
      <listitem>
       <para>Pull some data into a buffer</para>
      </listitem>
      <listitem>
       <para>Loop over the values in the buffer, incrementing a counter on each newline character</para>
      </listitem>
      <listitem>
       <para>Return to 2</para>
      </listitem>
      <listitem>
       <para>Close the file</para>
      </listitem>
     </orderedlist>
     <para>Notice that your code is explicitly calling out to other code and that code is returning
   control back to your code. You have retained full control of the flow of execution of your
   program. The conduit approach we've seen so far does <emphasis>not</emphasis> work this way. Instead, you
   would:</para>
     <orderedlist>
      <listitem>
       <para>Write a sink that counts newlines and adds the result to an accumulator.</para>
      </listitem>
      <listitem>
       <para>Connect the sink to a source</para>
      </listitem>
     </orderedlist>
     <para>There's no doubt in my mind that this is an easier approach. You don't have to worry about
   opening and closing files or pulling data from the file. Instead, the data you need to process is
   simply presented to you. This is the advantage of IoC: you can focus on specifically your piece
   of the code.</para>
     <para>We use this IoC approach all over Haskell: for example, instead of <literal>readMVar</literal>
   and <literal>putMVar</literal>, you can use <literal>withMVar</literal>. Don't bother with
    <literal>openFile</literal> and <literal>closeFile</literal>, just use <literal>withFile</literal> and
   pass in a function that uses the <literal>Handle</literal>. Even C has a version of this: why
    <literal>malloc</literal> and <literal>free</literal> when you could just
   <literal>alloca</literal>?</para>
     <para>Actually, that last one is a huge red herring. Of course you can't just use
    <literal>alloca</literal> for everything. <literal>alloca</literal> only allocates memory locally on
   the stack, not dynamically on the heap. There's no way to return your allocated memory outside
   the current function.</para>
     <para>But actually, the same restriction applies to the whole family of <literal>with</literal>
   functions: you can never return an allocated resource outside of the "block". Usually this works
   out just fine, but we need to recognize that this <emphasis>is</emphasis> a change in how we structure our
   programs. Often times, with simple examples, this is a minor change. However, in larger settings
   this can become very difficult to manage, bordering on impossible at times.</para>
     <section>
      <title>A web server</title>
      <para/>
      <para>Let's say we're going to write a web server. We're going to use the following low-level
   operations:</para>
      <programlisting language="haskell">data Socket
recv    :: Socket -&gt; Int -&gt; IO ByteString -- returns empty when the socket is closed
sendAll :: Socket -&gt; ByteString -&gt; IO ()</programlisting>
      <para>We're up to the part where we need to implement the function <literal>handleConn</literal> that
   handles an individual connection. It will look something like this:</para>
      <programlisting language="haskell">data Request  -- request headers, HTTP version, etc
data Response -- status code, response headers, resposne body
type Application = Request -&gt; IO Response
handleConn :: Application -&gt; Socket -&gt; IO ()</programlisting>
      <para>What does our <literal>handleConn</literal> need to do? In broad strokes:</para>
      <orderedlist>
       <listitem>
        <para>Parse the request line</para>
       </listitem>
       <listitem>
        <para>Parse the request headers</para>
       </listitem>
       <listitem>
        <para>Construct the <literal>Request</literal> value</para>
       </listitem>
       <listitem>
        <para>Pass <literal>Request</literal> to the <literal>Application</literal> and get back a
     <literal>Response</literal>
        </para>
       </listitem>
       <listitem>
        <para>Send the <literal>Response</literal> over the <literal>Socket</literal>
        </para>
       </listitem>
      </orderedlist>
      <para>We start off by writing steps 1 and 2 manually, without using conduits. We'll do this very
   simply and just assume three space-separated strings. We end up with something that looks
   like:</para>
      <programlisting language="haskell">data RequestLine = RequestLine ByteString ByteString ByteString

parseRequestLine :: Socket -&gt; IO RequestLine
parseRequestLine socket = do
    bs &lt;- recv socket 4096
    let (method:path:version:ignored) = S8.words bs
    return $ RequestLine method path version</programlisting>
      <para>There are two issues here: it doesn't handle the case where there are less than three words in
   the chunk of data, and it throws away any extra data. We can definitely solve both of these
   issues manually, but it's very tedious. It's much easier to implement this in terms of
   conduits.</para>
      <programlisting language="haskell">import Data.ByteString (ByteString)
import qualified Data.ByteString as S
import Data.Conduit
import qualified Data.Conduit.Binary as CB
import qualified Data.Conduit.List as CL

data RequestLine = RequestLine ByteString ByteString ByteString

parseRequestLine :: Sink ByteString IO RequestLine
parseRequestLine = do
    let space = toEnum $ fromEnum ' '
    let getWord = do
            CB.dropWhile (== space)
            bss &lt;- CB.takeWhile (/= space) =$ CL.consume
            return $ S.concat bss

    method &lt;- getWord
    path &lt;- getWord
    version &lt;- getWord
    return $ RequestLine method path version
</programlisting>
      <para>This means that our code will automatically be supplied with more data as it comes in, and any
   extra data will automatically be buffered in the <literal>Source</literal>, ready for the next time
   it's used. Now we can easily structure our program together, demonstrating the power of the
   conduits approach:</para>
      <programlisting language="haskell">import Data.ByteString (ByteString)
import Data.Conduit
import Data.Conduit.Network (sourceSocket)
import Control.Monad.IO.Class (liftIO)
import Network.Socket (Socket)

data RequestLine = RequestLine ByteString ByteString ByteString
type Headers = [(ByteString, ByteString)]
data Request = Request RequestLine Headers
data Response = Response
type Application = Request -&gt; IO Response

parseRequestHeaders :: Sink ByteString IO Headers
parseRequestHeaders = undefined

parseRequestLine :: Sink ByteString IO RequestLine
parseRequestLine = undefined

sendResponse :: Socket -&gt; Response -&gt; IO ()
sendResponse = undefined

handleConn :: Application -&gt; Socket -&gt; IO ()
handleConn app socket = do
    req &lt;- runResourceT $ sourceSocket socket $$ do
        requestLine &lt;- parseRequestLine
        headers &lt;- parseRequestHeaders
        return $ Request requestLine headers
    res &lt;- liftIO $ app req
    liftIO $ sendResponse socket res
</programlisting>
     </section>
     <section>
      <title>Whither the request body?</title>
      <para/>
      <para>This is all great, until we realize <emphasis>we can't read the request body</emphasis>. The
    <literal>Application</literal> is simply given the <literal>Request</literal>, and lives in the
    <literal>IO</literal> monad. It has no access whatsoever to the incoming stream of data.</para>
      <para>There's an easy fix for this actually: have the <literal>Application</literal> live in the
    <literal>Sink</literal> monad. This is the very approach we took with
    <function>enumerator</function>-based WAI 0.4. However, there are two problems:</para>
      <itemizedlist>
       <listitem>
        <para>People find it confusing. What people <emphasis>expect</emphasis> is that the <literal>Request</literal>
    value would have a <literal>requestBody</literal> value of type <literal>Source</literal>.</para>
       </listitem>
       <listitem>
        <para>This makes certain kinds of usage incredibly difficult. For example, trying to write an HTTP
    proxy combining WAI and <literal>http-enumerator</literal> proved to be almost impossible.</para>
       </listitem>
      </itemizedlist>
      <para>This is the downside of inversion of control. Our code wants to be in control. It wants to be
   given something to pull from, something to push to, and run with it. We need some solution to the
   problem.</para>
      <note>
       <para>If you think that the situation I described with the proxy isn't so bad, it's because I've
   gone easy on the details. We also need to take into account streaming the response body, and the
   streaming needs to happen on both the client and server side.</para>
      </note>
      <para>The simplest solution would be to just create a new <literal>Source</literal> and pass that to
   the <literal>Application</literal>. Unfortunately, this will cause problems with our buffering. You
   see, when we connect our source to the <literal>parseRequestLine</literal> and
    <literal>parseRequestHeaders</literal> sinks, it made a call to <literal>recv</literal>. If the data
   it received was not enough to cover all of the headers, it would issue another call. When it had
   enough data, it would stop. However, odds are that it didn't stop <emphasis>exactly</emphasis> at the end of
   the headers. It likely consumed a bit of the request body as well.</para>
      <para>If we just create a new source and pass that to the request, it will be missing the beginning
   of the request body. We need some way to pass that buffered data along.</para>
     </section>
    </section>
    <section>
     <title>BufferedSource</title>
     <para/>
     <para>And so we finally get to introduce the last data type in conduits:
    <literal>BufferedSource</literal>. This is an abstract data type, but all it really does is keep a
   mutable reference to a buffer and an underlying <literal>Source</literal>. In order to create one
   of these, you use the <literal>bufferSource</literal> function.</para>
     <programlisting language="haskell">bufferSource::Resourcem =&gt;Sourcem a -&gt;ResourceTm (BufferedSourcem a)</programlisting>
     <para>This one little change is what allows us to easily solve our web server dilemna. Instead of
   connecting a <literal>Source</literal> to our parsing <literal>Sink</literal>s, we use a
    <literal>BufferedSource</literal>. At the end of each connection, any leftover data is put back on
   the buffer. For our web server case, we can now create a <literal>BufferedSource</literal>, use
   that to read the request line and headers, and then pass that same
    <literal>BufferedSource</literal> to the application for reading the request body.</para>
    </section>
    <section>
     <title>Typeclass</title>
     <para/>
     <para>We want to be able to connect a buffered source to a sink, just like we would a regular source.
   We would also like to be able to fuse it to a conduit. In order to make this convenient, conduit
   has a typeclass, <literal>IsSource</literal>. There are instances provided for both
    <literal>Source</literal> and <literal>BufferedSource</literal>. Both the connect
    (<literal>$$</literal>) and left-fuse (<literal>$=</literal>) operators use this typeclass.</para>
     <para>There's one "gotcha" in the <literal>BufferedSource</literal> instance of this typeclass, so
   let's explain it. Suppose we want to write a file copy function, without any buffering. This is a
   fairly standard usage of conduits:</para>
     <programlisting language="haskell">sourceFile input $$ sinkFile output</programlisting>
     <para>When this line is run, both the input and output files are opened, the data is copied, and then
   both files are closed. Let's change this example slightly to use buffering:</para>
     <programlisting language="haskell">bsrc &lt;- bufferSource $ sourceFile input
bsrc $$ isolate 50 =$ sinkFile output1
bsrc $$ sinkFile output2</programlisting>
     <para>When is the input file opened and closed? The opening occurs on the first line, when buffering
   the source. And if we follow the normal rules from sources, the file should be closed after the
   second line. However, if we did that, we couldn't reuse <literal>bsrc</literal> for line 3!</para>
     <para>So instead, <literal>$$</literal> does <emphasis>not</emphasis> close the file. As a result, you can pass a
   buffered source to as many actions as you want, without concerns that the file handle has been
   closed out from under you.</para>
     <note>
      <para>If you remember from earlier, the invariant of a source is that it cannot be pulled from
   after it returns a <literal>Closed</literal> response. In order to allow you to work more easily
   with a <literal>BufferedSource</literal>, this invariant is relaxed. It is the responsibility of
   the <literal>BufferSource</literal> implementation to ensure that after the underlying
    <literal>Source</literal> is closed, it is never used again.</para>
     </note>
     <para>This presents one caveat: when you're finished with a buffered source, you should manually call
    <literal>bsourceClose</literal> on it. However, as usual, this is merely an optimization, as the
   source will automatically be closed when <literal>runResourceT</literal> is called.</para>
    </section>
    <section>
     <title>Recapping the web server</title>
     <para/>
     <para>So what exactly does our web server look like now?</para>
     <programlisting language="haskell">import Data.ByteString (ByteString)
import Data.Conduit
import Data.Conduit.Network (sourceSocket)
import Control.Monad.IO.Class (liftIO)
import Network.Socket (Socket)

data RequestLine = RequestLine ByteString ByteString ByteString
type Headers = [(ByteString, ByteString)]
data Request = Request RequestLine Headers (BufferedSource IO ByteString)
data Response = Response
type Application = Request -&gt; ResourceT IO Response

parseRequestHeaders :: Sink ByteString IO Headers
parseRequestHeaders = undefined

parseRequestLine :: Sink ByteString IO RequestLine
parseRequestLine = undefined

sendResponse :: Socket -&gt; Response -&gt; IO ()
sendResponse = undefined

handleConn :: Application -&gt; Socket -&gt; IO ()
handleConn app socket = runResourceT $ do
    bsrc &lt;- bufferSource $ sourceSocket socket
    requestLine &lt;- bsrc $$ parseRequestLine
    headers &lt;- bsrc $$ parseRequestHeaders
    let req = Request requestLine headers bsrc
    res &lt;- app req
    liftIO $ sendResponse socket res
</programlisting>
     <para>We've made a few minor changes. Firstly, the <literal>Application</literal> now lives in the
    <literal>ResourceT IO</literal> monad. This isn't strictly necessary, but it's very convenient:
   the application can now register cleanup actions that will only take place after the response has
   been fully sent to the client.</para>
     <para>But the major changes are in the <literal>handleConn</literal> function. We now start off by
   buffering our source. This buffered source is then used twice in our function, and then passed
   off to the application.</para>
    </section>
   </section>
  </chapter>